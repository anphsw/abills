#!perl
use strict;
use warnings 'FATAL' => 'all';
use v5.16;

use Abills::Base qw/_bp in_array load_pmodule2/;
use Abills::Experimental;
use JSON qw/to_json from_json/;
use Maps;

our ($html, %lang, %conf, $admin, $db, @CABLECAT_EXTRA_COLORS, %permissions);

our Maps $Maps;

if ( $FORM{json} ) {
  _bp('', '', { SET_ARGS => { TO_CONSOLE => 1 } });
}

# DB module is available
exit if (
  form_purchase_module(
    {
      HEADER          => $user->{UID},
      MODULE          => 'Cablecat',
      REQUIRE_VERSION => 7.49,
    }
  )
);

# Using selects from Maps module
load_module('Maps', $html);

if ( !defined $Maps ) {
  $Maps = Maps->new($db, $admin, \%conf);
}

require Cablecat;
Cablecat->import();
our Cablecat $Cablecat = Cablecat->new($db, $admin, \%conf);

require Equipment;
Equipment->import();
my $Equipment = Equipment->new($db, $admin, \%conf);

my %CONNECTION_TYPES = (
  1 => 'fiber',
  2 => 'equipment',
  3 => 'point_id',
  4 => 'splitter',
  5 => 'client',
  6 => 'cross'
);
if ( !in_array('Equipment', \@MODULES) ) {
  delete $CONNECTION_TYPES{2};
}

my @CABLECAT_COLORS = (
  'fcfefc', # white
  '04fefc', # sea
  'fcfe04', # yellow
  '048204', # green
  '840204', # brown
  'fc0204', # red
  'fc9a04', # orange
  'fc9acc', # pink
  '848284', # gray
  '0402fc', # blue
  '840284', # violet
  '040204', # black
  '04fe04', # yellowgreen
  '9cce04', # olive
  'fcfe9c', # beige
  'dbefdb', # natural
  'fde910', # lemon
  '9c3232',    # cherry
);

$conf{CABLECAT_CLEAR_DEFAULT_COLORS} //= 0;
$conf{CABLECAT_COLOR_SCHEME_NUMBERS} //= 0;

# Allowing to add colors via config.pl
if ( @CABLECAT_EXTRA_COLORS ) {
  if ( $conf{CABLECAT_CLEAR_DEFAULT_COLORS} ) {
    @CABLECAT_COLORS = @CABLECAT_EXTRA_COLORS;
  }
  else {
    @CABLECAT_COLORS = (@CABLECAT_COLORS, @CABLECAT_EXTRA_COLORS);
  }
}

our %MAP_TYPE_ID = (
  'WELL'      => 1,
  'WIFI'      => 2,
  'BUILD'     => 3,
  'ROUTE'     => 4,
  'CONNECTER' => 5,
  'SPLITTER'  => 6,
  'CABLE'     => 7,
  'EQUIPMENT' => 8,
  'PILLAR'    => 9,
);

our %MAP_LAYER_ID = (
  CABLE => 10,
  WELL  => 11
);

if ( $permissions{3} ) {
  require Cablecat::Reports;
}

require Cablecat::ResultFormerFilters;
require Cablecat::Selects;

our %CROSS_CROSS_TYPE = (
  1 => $lang{RACK_MOUNTABLE},
  2 => $lang{RETRACTABLE}
);

our %CROSS_PANEL_TYPE = (
  1 => $lang{SOLID_PANEL},
  2 => $lang{CHANGEABLE_PLANKS},
  3 => "$lang{SOLID_PANEL} SC DUPLEX"
);

our %CROSS_PORT_TYPE = (
  1 => 'SC',
  2 => 'SC DUPLEX',
  3 => 'FC',
  4 => 'ST',
  5 => 'LC (duplex)'
);

our %CROSS_POLISH_TYPE = (
  1 => 'UPC',
  2 => 'APC'
);

our %CROSS_FIBER_TYPE = (
  1 => '9/125',
  2 => '50/125',
  3 => '62,5/125',
  4 => 'OM3'
);

require Cablecat::Configure;

#**********************************************************
=head2 cablecat_main()

=cut
#**********************************************************
sub cablecat_main {
  
  my $menu_button = sub {
    my ($lang_name, $function_name, $class) = @_;
    $html->element('li', $html->button($lang_name, "index=" . get_function_index($function_name)),
      {
        class => 'list-group-item ' . ($class ? ('list-group-item-' . $class) : '')
      }
    );
  };
  
  $html->tpl_show(
    _include('cablecat_main', 'Cablecat'),
    {
      GO_TO_MAP_BTN     => $menu_button->($lang{MAP}, 'maps_edit', 'success'),
      COLOR_SCHEMES_BTN => $menu_button->($lang{COLOR_SCHEMES}, 'cablecat_color_schemes'),
      CABLES_BTN        => $menu_button->($lang{CABLES}, 'cablecat_cables'),
      WELLS_BTN         => $menu_button->($lang{WELLS}, 'cablecat_wells'),
      SPLITTERS_BTN     => $menu_button->($lang{SPLITTERS}, 'cablecat_splitters'),
      CONNECTERS_BTN    => $menu_button->($lang{CONNECTERS}, 'cablecat_connecters'),
      COMMUTATIONS_BTN  => $menu_button->($lang{COMMUTATIONS}, 'cablecat_commutations'),
      CROSSES_BTN       => $menu_button->($lang{CROSSES}, 'cablecat_crosses')
    }
  );
  
  return 1;
}


#**********************************************************
=head2 cablecat_commutation()

=cut
#**********************************************************
sub cablecat_commutation {
  
  #  _bp('', 'TEST');
  #  return $html->tpl_show(_include('cablecat_temp', 'Cablecat'));
  #
  my %TEMPLATE_ARGS = ();
  my $show_add_form = $FORM{add_form} || 0;
  
  # Handling AJAX requests
  if ( $FORM{commutation} ) {
    return &cablecat_commutation_ajax;
  }
  elsif ( $FORM{operation} ) {
    return &cablecat_commutation_operations;
  }
  
  if ( $FORM{ID} ) {
    my $tp_info = $Cablecat->commutations_info($FORM{ID});
    if ( !_error_show($Cablecat) ) {
      %TEMPLATE_ARGS = %{$tp_info};
      $show_add_form = 1;
    }
  }
  
  return 1 if ( $FORM{MESSAGE_ONLY} );
  
  my $cable_ids = $TEMPLATE_ARGS{CABLE_IDS} || $FORM{CABLE_IDS};
  
  if ( $show_add_form && $cable_ids ) {
    my @cable_ids = split(/,\s?/, $cable_ids);
    
    if ( defined $TEMPLATE_ARGS{ID} ) {
      
      my $cables = _cablecat_commutation_cables_prepare_json($cable_ids, { COMMUTATION_ID => $TEMPLATE_ARGS{ID} });
      return 0 if (!$cables);
      
      my $splitters = _cablecat_commutation_splitters(undef, { COMMUTATION_ID => $TEMPLATE_ARGS{ID} });
      return 0 if (!$splitters);
      
      my $equipment = _cablecat_commutation_equipment(undef, { COMMUTATION_ID => $TEMPLATE_ARGS{ID} });
      return 0 if (!$equipment);
      
      $TEMPLATE_ARGS{INFO_TABLE} = _cablecat_commutation_info_table($TEMPLATE_ARGS{ID}, \%TEMPLATE_ARGS);
      
      # Cable links
      my $links_list = $Cablecat->commutation_links_list({
        CABLE_IDS        => \@cable_ids,
        SHOW_ALL_COLUMNS => 1,
        COLS_UPPER       => 0,
        PAGE_ROWS        => 500000
      });
      _error_show($Cablecat);
      
      $TEMPLATE_ARGS{LINKS} = to_json(
        [
          map {
            $_->{geometry} = from_json($_->{geometry}) if ( $_->{geometry} );
            
            # Commutation works with indexes
            $_->{fiber_num_1} = $_->{fiber_num_1} - 1 if ( $_->{fiber_num_1} );
            $_->{fiber_num_2} = $_->{fiber_num_2} - 1 if ( $_->{fiber_num_2} );
            $_;
          } @{$links_list}
        ]
      );
      
      # Other links
      my $com_links_list = $Cablecat->links_list({
        COMMUTATION_ID   => $TEMPLATE_ARGS{ID},
        SHOW_ALL_COLUMNS => 1,
        COLS_UPPER       => 0,
        PAGE_ROWS        => 500000
      });
      _error_show($Cablecat);
      
      $TEMPLATE_ARGS{COMMUTATION_LINKS} = to_json(
        [
          map {
            $_->{geometry} = from_json($_->{geometry}) if ( $_->{geometry} );
            $_->{fiber_num_1} -= 1 if ( $_->{fiber_num_1} );
            $_->{fiber_num_2} -= 1 if ( $_->{fiber_num_2} );
            $_;
          } @{$com_links_list}
        ]
      );
      
      $TEMPLATE_ARGS{CABLES} = join(';', to_json($cables));
      $TEMPLATE_ARGS{SPLITTERS} = join(';', to_json($splitters));
      $TEMPLATE_ARGS{EQUIPMENT} = join(';', to_json($equipment));
    }
    
    $html->tpl_show(_include('cablecat_commutation', 'Cablecat'), \%TEMPLATE_ARGS);
  }
  
  return 1;
}

#**********************************************************
=head2 cablecat_commutation_operations()

=cut
#**********************************************************
sub cablecat_commutation_operations {
  return 0 unless ( $FORM{COMMUTATION_ID} );
  
  my $info = $Cablecat->commutations_info($FORM{COMMUTATION_ID});
  
  if ( !$FORM{entity} ) {
  
  }
  elsif ( $FORM{entity} eq 'CABLE' ) {
    return cablecat_commutation_cables($info);
  }
  elsif ( $FORM{entity} eq 'SPLITTER' ) {
    return cablecat_commutation_splitters($info);
  }
  elsif ( $FORM{entity} eq 'EQUIPMENT' ) {
    return cablecat_commutation_equipment($info);
  }
  
  if ( $FORM{operation} eq 'CLEAR_COMMUTATION' ) {
    $Cablecat->commutation_links_del({}, {
        COMMUTATION_ID => $FORM{COMMUTATION_ID},
      });
    $Cablecat->commutation_equipment_del({}, {
        COMMUTATION_ID => $FORM{COMMUTATION_ID},
      });
    #    $Cablecat->commutation_splitter_del({}, {
    #        COMMUTATION_ID => $FORM{COMMUTATION_ID},
    #      });
    show_result($Cablecat, "$lang{DELETED} $lang{ALL}");
  }
  elsif ( $FORM{operation} eq 'CONNECT_BY_NUMBERS' ) {
    # Get cables for commutation
    my $cables_list = $Cablecat->commutation_cables_list({
      COMMUTATION_ID => $FORM{COMMUTATION_ID},
      CABLE_ID       => '_SHOW',
      CABLE_NAME     => '_SHOW',
    });
    _error_show($Cablecat) and return 0;
    
    my $has_only_to_cables = $Cablecat->{TOTAL} == 2;
    my $selected_1 = '';
    my $selected_2 = '';
    if ( $has_only_to_cables ) {
      $selected_1 = $cables_list->[0]->{cable_id};
      $selected_2 = $cables_list->[1]->{cable_id};
    }
    
    my $cable_1_select = make_select_from_list("CABLE_1", $cables_list,
      {
        SELECTED  => $selected_1,
        SEL_KEY   => 'cable_id',
        SEL_VALUE => 'cable_name'
      }
    );
    my $cable_2_select = make_select_from_list("CABLE_2", $cables_list,
      {
        SELECTED  => $selected_2,
        SEL_KEY   => 'cable_id',
        SEL_VALUE => 'cable_name'
      }
    );
    
    # Get number of fibers for each cable
    
    # Show template
    $html->tpl_show(
      _include('cablecat_commutation_connect_by_numbers', 'Cablecat'),
      {
        CABLE_1_SELECT => $cable_1_select,
        CABLE_2_SELECT => $cable_2_select
      }
    );
    
  }
  else {
    print "Wrong params";
  }
  
  return 1;
}

#**********************************************************
=head2 cablecat_commutation_cables()

=cut
#**********************************************************
sub cablecat_commutation_cables {
  my ($commutation) = @_;
  
  if ( $FORM{operation} eq 'LIST' && $FORM{WELL_ID} ) {
    my $cables_inputs = _cablecat_well_cables_checkbox_form(
      $FORM{WELL_ID},
      {
        SKIP => [ split(',\s?', $commutation->{CABLE_IDS}) ]
      }
    );
    
    if ( !$cables_inputs || (ref $cables_inputs eq 'ARRAY' && !scalar @{$cables_inputs}) ) {
      $html->message('err', $lang{CABLES} . ' ' . $lang{WELL} . '_#' . $FORM{WELL_ID}, $lang{NO_DATA});
      return 0;
    }
    
    $html->tpl_show(_include('cablecat_commutation_cable_add_modal', 'Cablecat'), {
        CABLES_CHECKBOXES => join($FORM{json} ? ', ' : '', @{$cables_inputs}),
        SUBMIT_BTN_NAME   => $lang{ADD},
        SUBMIT_BTN_ACTION => 'change',
        %FORM
      }
    );
  }
  elsif ( $FORM{operation} eq 'ADD' ) {
    my $info = $Cablecat->commutations_info($FORM{COMMUTATION_ID});
    my $current_cable_ids = [ split(',\s?', $info->{CABLE_IDS}) ];
    
    foreach my $cable_id ( split(',\s?', $FORM{CABLE_IDS}) ) {
      
      # Already exists
      next if ( in_array($cable_id, $current_cable_ids) );
      
      $Cablecat->commutation_cables_add({
        CABLE_ID       => $cable_id,
        CONNECTER_ID   => $FORM{CONNECTER_ID},
        COMMUTATION_ID => $FORM{COMMUTATION_ID}
      });
      
      show_result($Cablecat, $lang{ADDED});
      
      $html->redirect('?index=' . (get_function_index('cablecat_commutation') . '&ID=' . $FORM{COMMUTATION_ID}));
    }
  }
  elsif ( $FORM{operation} eq 'DELETE' && $FORM{ID} ) {
    $Cablecat->commutation_cables_del({}, {
        commutation_id => $FORM{COMMUTATION_ID},
        connecter_id   => $FORM{CONNECTER_ID},
        cable_id       => $FORM{ID}
      });
    
    show_result($Cablecat, $lang{DEL});
  }
  
}


#**********************************************************
=head2 cablecat_commutation_splitters()

=cut
#**********************************************************
sub cablecat_commutation_splitters {
  if ( $FORM{operation} eq 'LIST' ) {
    my $splitters_for_well = $Cablecat->splitters_list({
      ID         => '_SHOW',
      TYPE       => '_SHOW',
      FIBERS_IN  => '_SHOW',
      FIBERS_OUT => '_SHOW',
      WELL_ID    => $FORM{WELL_ID},
    });
    
    my $splitters_select = $html->form_select('SPLITTER_ID', {
        SEL_LIST    => [ map {$_->{name} = $_->{type} . '_#' . $_->{id};
          $_} @{$splitters_for_well} ],
        SEL_OPTIONS => { '' => '' }
      });
    
    $html->tpl_show(_include('cablecat_commutation_splitter_add_modal', 'Cablecat'), {
        SPLITTERS_SELECT  => $splitters_select,
        SUBMIT_BTN_NAME   => $lang{ADD},
        SUBMIT_BTN_ACTION => 'change',
        %FORM
      }
    );
  }
  elsif ( $FORM{operation} eq 'ADD' ) {
    # Do splitter adding logic
    my $splitter_info = $Cablecat->splitters_info($FORM{SPLITTER_ID});
    _error_show($Cablecat);
    
    # Set splitter commutation
    if ( !$splitter_info->{commutation_id} || $splitter_info->{commutation_id} ne $FORM{COMMUTATION_ID} ) {
      $Cablecat->splitters_change({
        ID             => $FORM{SPLITTER_ID},
        COMMUTATION_ID => $FORM{COMMUTATION_ID}
      })
    }
    
    show_result($Cablecat, $lang{ADDED});
  }
  elsif ( $FORM{operation} eq 'DELETE' && $FORM{ID} ) {
    $Cablecat->splitters_change({
      ID             => $FORM{ID},
      WELL_ID        => '0',
      COMMUTATION_X  => '0',
      COMMUTATION_Y  => '0',
      COMMUTATION_ID => '0'
    });
    
    show_result($Cablecat, $lang{DEL});
  }
  elsif ( $FORM{operation} eq 'SAVE_COORDS' ) {
    $Cablecat->splitters_change({
      ID            => $FORM{ID},
      COMMUTATION_X => $FORM{X},
      COMMUTATION_Y => $FORM{Y},
    });
    show_result($Cablecat, $lang{CHANGED});
  }
}


#**********************************************************
=head2 cablecat_commutation_equipment()

=cut
#**********************************************************
sub cablecat_commutation_equipment {
  if ( !in_array('Equipment', \@MODULES) ) {
    $html->message('warn', $lang{WARNING}, "$lang{MODULE} $lang{DISABLED} : 'Equipment'");
    return 0;
  }
  
  if ( $FORM{operation} eq 'LIST' ) {
    
    # Will remove equipment that already is on another commutations
    my @equipment_on_commutation_ids = $Cablecat->commutation_equipment_ids();
    _error_show($Cablecat) and return 0;
    
    my $equipment_list = $Equipment->_list({
      NAS_NAME  => '_SHOW',
      NAS_ID    => join(',', map {"!$_"}  @equipment_on_commutation_ids),
      COLS_NAME => 1,
      PAGE_ROWS => 10000
    });
    _error_show($Equipment) and return 0;
    
    if ( !$equipment_list || ref($equipment_list) ne 'ARRAY' || !scalar(@{$equipment_list}) ) {
      $html->message("warn", $lang{WARNING}, $lang{NO_FREE_EQUIPMENT});
      print function_button("$lang{GO_TO} $lang{EQUIPMENT}", 'equipment_list');
      return 0;
    }
    
    my $equipment_select = make_select_from_list('EQUIPMENT_ID', $equipment_list, {
        NO_ID     => 0,
        SEL_KEY   => 'nas_id',
        SEL_VALUE => 'nas_name',
        MAIN_MENU => get_function_index('equipment_list')
      });
    
    $html->tpl_show(_include('cablecat_commutation_equipment_add_modal', 'Cablecat'), {
        EQUIPMENT_SELECT  => $equipment_select,
        SUBMIT_BTN_NAME   => $lang{ADD},
        SUBMIT_BTN_ACTION => 'change',
        %FORM
      }
    );
  }
  elsif ( $FORM{operation} eq 'ADD' ) {
    # Do equipment adding logic
    my $commutation_info = $Cablecat->commutation_equipment_info(undef, { NAS_ID => $FORM{EQUIPMENT_ID} });
    
    my $not_added_to_commutation =
      !$commutation_info
        || !$commutation_info->{commutation_id}
        || $commutation_info->{commutation_id} ne $FORM{COMMUTATION_ID};
    
    # Set splitter commutation
    if ( $not_added_to_commutation ) {
      
      # Check if have ports count defined
      my $equipment_info = $Equipment->_info($FORM{EQUIPMENT_ID});
      my $ports_count = $equipment_info->{PORTS};
      
      if ( !$ports_count || $ports_count eq '0' ) {
        $html->message('err', $lang{ERROR}, $lang{NO_PORTS_COUNT});
        return 0;
      }
      
      $Cablecat->commutation_equipment_add({
        NAS_ID         => $FORM{EQUIPMENT_ID},
        COMMUTATION_ID => $FORM{COMMUTATION_ID}
      });
      show_result($Cablecat, $lang{ADDED});
    }
    else {
      $html->message('err', $lang{ERROR}, $lang{ALREADY_ON_ANOTHER_COMMUTATION});
      return 0;
    }
  }
  elsif ( $FORM{operation} eq 'DELETE' ) {
    $Cablecat->commutation_equipment_del({}, { NAS_ID => $FORM{ID} });
    show_result($Cablecat, $lang{DEL});
  }
  elsif ( $FORM{operation} eq 'SAVE_COORDS' ) {
    $Cablecat->{debug} = 1;
    $Cablecat->commutation_equipment_change({
      _CHANGE_PARAM => 'NAS_ID',
      
      NAS_ID        => $FORM{ID},
      COMMUTATION_X => $FORM{X},
      COMMUTATION_Y => $FORM{Y},
    });
    show_result($Cablecat, $lang{CHANGED});
  }
}


#**********************************************************
=head2 cablecat_commutation_ajax()

=cut
#**********************************************************
sub cablecat_commutation_ajax {
  return unless ( $FORM{commutation} );
  
  my @link_required_params = qw/
    COMMUTATION_ID
    ELEMENT_1_TYPE ELEMENT_1_ID
    ELEMENT_2_TYPE ELEMENT_2_ID
    FIBER_NUM_1 FIBER_NUM_2
    /;
  
  # Add link
  if ( $FORM{add} ) {
    
    # Check we have all arguments to add link
    if ( grep {!$FORM{$_}} @link_required_params ) {
      $html->message('err', 'No param', $_);
      return 0;
    }
    
    my $link_id = 0;
    # Cable links are added in `cablecat_commutation_links`
    if ( $FORM{ELEMENT_1_TYPE} eq 'CABLE' && $FORM{ELEMENT_2_TYPE} eq 'CABLE' ) {
      
      $link_id = $Cablecat->commutation_links_add({
        COMMUTATION_ID => $FORM{COMMUTATION_ID},
        CABLE_ID_1     => $FORM{ELEMENT_1_ID},
        CABLE_ID_2     => $FORM{ELEMENT_2_ID},
        FIBER_NUM_1    => $FORM{FIBER_NUM_1} + 1,
        FIBER_NUM_2    => $FORM{FIBER_NUM_2} + 1,
      });
    }
    # All other links are added in `cablecat_links`
    else {
      $link_id = $Cablecat->links_add({
        %FORM,
        FIBER_NUM_1 => $FORM{FIBER_NUM_1} + 1,
        FIBER_NUM_2 => $FORM{FIBER_NUM_2} + 1,
      });
    }
    show_result($Cablecat, $lang{ADDED}, '', { ID => $link_id });
  }
  
  if ( $FORM{del} ) {
    if ( $FORM{cables_link} ) {
      $Cablecat->commutation_links_del({ ID => $FORM{del} });
    }
    else {
      $Cablecat->links_del({ ID => $FORM{del} });
    }
    show_result($Cablecat, $lang{DEL});
  }
  
  if ( $FORM{change} ) {
    if ( $FORM{cables_link} ) {
      $Cablecat->commutation_links_change({ %FORM, ID => $FORM{change} });
    }
    else {
      $Cablecat->links_change({ %FORM, ID => $FORM{change} });
    }
    show_result($Cablecat, $lang{CHANGED});
  }
  
  return 1;
}


#**********************************************************
=head2 _cablecat_commutation_info_table()

=cut
#**********************************************************
sub _cablecat_commutation_info_table {
  my ($commutation_id, $attr) = @_;
  
  my $commutation;
  if ( $attr->{ID} ) {
    $commutation = $attr;
  }
  elsif ( $commutation_id ) {
    $commutation = $Cablecat->commutation_info($commutation_id);
    _error_show($Cablecat);
  }
  else {
    return '';
  }
  
  my $table = $html->table({
    width       => '100%',
    caption     => '',
    title_plain => [ '#', $lang{WELL}, $lang{CONNECTER}, $lang{ADDRESS}, $lang{CREATED} ],
    qs          => $pages_qs,
    ID          => 'CABLECAT_COMMUTATION_INFO_ID',
  });
  
  if ( $commutation->{WELL_ID} ) {
    $commutation->{ADDRESS} = _cablecat_address_for_well_id($commutation->{WELL_ID});
  }
  
  $table->addrow(
    $commutation->{ID},
    _cablecat_result_former_named_chg_link_filter($commutation->{WELL}, {
        VALUES => {
          FUNCTION   => 'cablecat_wells',
          PARAM_NAME => 'WELL_ID',
          WELL_ID    => $commutation->{WELL_ID}
        }
      }),
    _cablecat_result_former_named_chg_link_filter($commutation->{CONNECTER}, {
        VALUES => {
          FUNCTION     => 'cablecat_connecters',
          PARAM_NAME   => 'CONNECTER_ID',
          CONNECTER_ID => $commutation->{CONNECTER_ID}
        }
      }),
    $commutation->{ADDRESS} || '',
    $commutation->{CREATED} || '!',
  );
  
  return $table->show();
}

#**********************************************************
=head2 cablecat_maps_layers()

=cut
#**********************************************************
sub cablecat_maps_layers {
  return {
    LAYERS      => [
      {
        id            => 10,
        name          => 'CABLES',
        lang_name     => $lang{CABLES},
        module        => 'Cablecat',
        structure     => 'POLYLINE',
        clustering    => 0,
        add_func      => 'cablecat_cables',
        custom_params => {
          OBJECT_TYPE_ID               => $MAP_TYPE_ID{CABLE},
          SAVE_AS_GEOMETRY             => 1,
          CALCULATE_PARAMS_JS_FUNCTION => 'findClosestWellsForCable'
        }
      }, {
        id            => 11,
        name          => 'WELLS',
        lang_name     => $lang{WELLS},
        module        => 'Cablecat',
        structure     => 'MARKER',
        clustering    => 1,
        add_func      => 'cablecat_wells',
        custom_params => {
          OBJECT_TYPE_ID => $MAP_TYPE_ID{WELL}
        }
      }
    ],
    SCRIPTS     => [ '/styles/default_adm/js/maps/modules/cablecat.js' ],
    EXPORT_FUNC => {
      10 => 'cablecat_maps_cables',
      11 => 'cablecat_maps_wells',
    }
  }
}

#**********************************************************
=head2 cablecat_maps_cables()

=cut
#**********************************************************
sub cablecat_maps_cables {
  
  my $cables_list = $Cablecat->cables_list({
    POINT_ID          => $FORM{OBJECT_ID} || $FORM{POINT_ID} || '!',
    NAME              => '_SHOW',
    CABLE_TYPE        => '_SHOW',
    WELL_1            => '_SHOW',
    WELL_2            => '_SHOW',
    WELL_1_ID         => '_SHOW',
    WELL_2_ID         => '_SHOW',
    OUTER_COLOR       => '_SHOW',
    LINE_WIDTH        => '_SHOW',
    LENGTH            => '_SHOW',
    LENGTH_CALCULATED => '_SHOW',
    COMMENTS          => '_SHOW',
    PAGE_ROWS         => 10000
  });
  _error_show($Cablecat);
  
  # Get all current active objects that cables are linked to
  my @object_ids = map {+ $_->{point_id}} @{$cables_list};
  
  # Joining for DB searching
  my $point_ids = join(';', @object_ids);
  
  my $points_list = $Maps->points_list({ ID => $point_ids, SHOW_ALL_COLUMNS => 1, EXTERNAL => 1, PAGE_ROWS => 10000 });
  my $layer_objects = _maps_get_layer_objects(10, { OBJECT_ID => $point_ids });
  
  # Sorting to hashes
  my $points_by_id = sort_array_to_hash($points_list, 'id');
  my $cable_by_point_id = sort_array_to_hash($cables_list, 'point_id');
  my $layer_objects_by_point_id = sort_array_to_hash($layer_objects, 'OBJECT_ID');
  
  # Caching indexes
  my $well_index = get_function_index('cablecat_wells');
  my $cables_index = get_function_index('cablecat_cables');
  
  my @objects_to_show = ();
  # Apply cable_info to geometric figures
  foreach ( @object_ids ) {
    my $cable = $cable_by_point_id->{$_};
    next if ( !$cable->{point_id} || !$cable->{id} );
    
    my $polyline = $layer_objects_by_point_id->{$_}->{POLYLINE};
    next if ( !$polyline->{id} );
    
    my $point = $points_by_id->{$_};
    next if ( !$point );
    
    $layer_objects_by_point_id->{$_}->{OBJECT_ID} = $point->{id};
    
    my $line_info = arrays_array2table([
      [ $lang{CABLE}, $html->button($cable->{name}, "index=$cables_index&chg=$cable->{id}", { target => '_blank' }) ],
      [ $lang{CABLE_TYPE}, $cable->{cable_type} ],
      [ "$lang{WELL} 1", ($cable->{well_1} && $cable->{well_1_id})
          ? $html->button($cable->{well_1}, "index=$well_index&chg=$cable->{well_1_id}", { target => '_blank' })
            . maps_show_object_button(11, $cable->{well_1_id})
          : $lang{NO}
      ],
      [ "$lang{WELL} 2", ($cable->{well_2} && $cable->{well_2_id})
          ? $html->button($cable->{well_2}, "index=$well_index&chg=$cable->{well_2_id}", { target => '_blank' })
            . maps_show_object_button(11, $cable->{well_2_id})
          : $lang{NO}
      ],
      [ $lang{LENGTH}, "$cable->{length}, ( $cable->{length_calculated} )" ],
      [ $lang{COMMENTS}, $point->{comments} ],
    ]);
    
    if ( $point->{planned} ) {
      $polyline->{strokeOpacity} = '0.5';
    }
    
    $layer_objects_by_point_id->{$_}->{ID} = $cable->{id};
    
    #TODO: check which case is required
    $polyline->{id} = $cable->{id};
    $polyline->{ID} = $cable->{id};
    
    $polyline->{name} = $cable->{name} || '';
    
    $polyline->{strokeColor} = $cable->{outer_color};
    $polyline->{strokeWeight} = $cable->{line_width} || 1;
    
    #TODO: localize split btn
    my $edit_buttons = qq{
      <button class="btn btn-default" title="$lang{ADD} $lang{WELL}" onclick="insert_well_on_cable($cable->{id})">
        <span class="glyphicon glyphicon-sound-stereo"></span>
      </button>
      <button class="btn btn-default btn-sm" title="Split cable" onclick="split_cable($cable->{id})">
        <div class="text-small">
          <span class="glyphicon glyphicon-arrow-left"></span>
          <span class="glyphicon glyphicon-arrow-right"></span>
        </div>
      </button>
      <button class="btn btn-danger" onclick="showRemoveConfirmModal({ layer_id : 10, id : $polyline->{object_id} })">
        <span class="glyphicon glyphicon-remove"></span><span>$lang{DEL}</span>
      </button>
    };
    
    $polyline->{INFOWINDOW} = $line_info . $edit_buttons;
    
    push @objects_to_show, $layer_objects_by_point_id->{$_};
  }
  
  return join ',', map {to_json($_, { utf8 => 0 })} @objects_to_show;
}

#**********************************************************
=head2 cablecat_maps_wells()

=cut
#**********************************************************
sub cablecat_maps_wells {
  my $wells_list = $Cablecat->wells_list({
    POINT_ID  => $FORM{OBJECT_ID} || $FORM{POINT_ID} || '!',
    NAME      => '_SHOW',
    TYPE_ID   => '_SHOW',
    ICON      => '_SHOW',
    COMMENTS  => '_SHOW',
    PAGE_ROWS => 10000
  });
  _error_show($Cablecat);
  
  my @object_ids = map {$_->{point_id}} @{$wells_list};
  
  my $point_ids = join(';', @object_ids);
  _error_show($Maps);
  
  my $points_list = $Maps->points_list({
    ID               => $point_ids,
    SHOW_ALL_COLUMNS => 1,
    NAME             => '_SHOW',
    ICON             => '_SHOW',
    TYPE             => '_SHOW',
    TYPE_ID          => '_SHOW',
    COORDX           => '!',
    COORDY           => '!',
    COLS_NAME        => 1,
    ADDRESS_FULL     => '_SHOW',
    EXTERNAL         => 1,
  });
  
  my $points_by_id = sort_array_to_hash($points_list);
  my $well_by_point_id = sort_array_to_hash($wells_list, 'point_id');
  my $wells_index = get_function_index('cablecat_wells');
  
  my @layer_objects = ();
  
  # Apply cable_info to geometric figures
  
  foreach ( @object_ids ) {
    my $well = $well_by_point_id->{$_};
    my $point = $points_by_id->{$_};
    
    my $icon_name = $well->{icon} || $point->{icon} || 'well_green';
    
    next if ( !($point->{coordx} && $point->{coordy}) );
    
    my $marker_info = arrays_array2table([
      [ $lang{WELL}, $html->button($well->{name}, "index=$wells_index&chg=$well->{id}", { target => '_blank' }) ],
      [ $lang{INSTALLED}, $point->{planned} ? $lang{NO} : $lang{YES} ],
      [ $lang{COMMENTS}, $point->{comments} ],
    ]);
    
    my $edit_buttons = qq{
      <button class="btn btn-danger" onclick="showRemoveConfirmModal({ layer_id : 11, id : $point->{id} })">
        <span class="glyphicon glyphicon-remove"></span><span>$lang{DEL}</span>
      </button>
    };
    
    $marker_info .= $edit_buttons;
    
    push @layer_objects, {
        ID        => $well->{id},
        OBJECT_ID => $point->{id},
        MARKER    => {
          OBJECT_ID => $point->{id},
          NAME      => $well->{name},
          ID        => $well->{id},
          COORDX    => $point->{coordy},
          COORDY    => $point->{coordx},
          INFO      => "$marker_info",
          TYPE      => "$icon_name",
          #          SIZE      => [37, 37],
          CENTERED  => 1,
        },
        LAYER_ID  => 11
      }
  }
  
  return join (',', map {to_json($_, { utf8 => 0 })} @layer_objects);
}

#**********************************************************
=head2 cablecat_maps_ajax()

=cut
#**********************************************************
sub cablecat_maps_ajax {
  
  if ( $FORM{SPLIT_CABLE} && $FORM{CABLE_ID} ) {
    push (@{$html->{JSON_OUTPUT}},
      {
        result => _cablecat_break_cable_in_two_parts($FORM{CABLE_ID})
      }
    );
  }
  
  return 1;
}

#**********************************************************
=head2 cablecat_link_info()

=cut
#**********************************************************
sub cablecat_link_info {
  my ($cable_id, $fiber_num, $direction) = @FORM{'CABLE_ID', 'FIBER_NUM', 'DIRECTION'};
  
  if ( $FORM{renew} ) {
    my $res = _cablecat_external_link_info($cable_id, $fiber_num, $direction);
    print _cablecat_link_info_cell($cable_id, $fiber_num, $direction, $res);
    return 1;
  }
  elsif ( $FORM{add} && $FORM{TYPE} ) {
    
    if ( $FORM{TYPE} == 1 ) {
      # Add fiber link
      $Cablecat->commutation_links_add({
        COMMUTATION_ID => $FORM{TO_COMMUTATION},
        CABLE_ID_1     => ($FORM{DIRECTION} ? $FORM{CABLE_ID} : $FORM{TO_CABLE_ID}),
        FIBER_NUM_1    => ($FORM{DIRECTION} ? $FORM{FIBER_NUM_1} : $FORM{TO_FIBER}),
        CABLE_ID_2     => ($FORM{DIRECTION} ? $FORM{TO_CABLE_ID} : $FORM{CABLE_ID}),
        FIBER_NUM_2    => ($FORM{DIRECTION} ? $FORM{TO_FIBER} : $FORM{FIBER_NUM_1}),
        DIRECTION      => $FORM{DIRECTION}
      });
    }
    else {
      $Cablecat->links_add(\%FORM);
    }
    
    show_result($Cablecat, "$lang{FIBER} $lang{LINK} $lang{ADDED}");
    return 1;
  }
  elsif ( $FORM{del} && $cable_id && $fiber_num && defined $direction ) {
    # Deleting is normally done via id, so passing 'undef' to use exended params del
    $Cablecat->links_del(undef, {
        CABLE_ID  => $cable_id,
        FIBER_NUM => $fiber_num,
        DIRECTION => $direction,
      });
    show_result($Cablecat, "$lang{FIBER} $lang{LINK} $lang{DELETED}");
  }
  elsif ( $FORM{request} && $FORM{TYPE} && exists $CONNECTION_TYPES{$FORM{TYPE}} ) {
    load_pmodule2('JSON');
    
    #*****************************************************
    # sub_make_select_options - form select options from list
    #
    #*****************************************************
    my $sub_make_select_options = sub {
      my ($list, $key_name, $value_name ) = @_;
      
      my @options = ({ name => '--', value => '--' });
      
      push (@options, map {{ name => $_->{$key_name}, value => $_->{$value_name} }} @{$list});
      
      # Return
      \@options;
    };
    
    #*****************************************************
    # select_next_option - return JSON select to extend search form
    #
    #*****************************************************
    my $select_next_option = sub {
      my ($name, $label, $list, $attr) = @_;
      my %res = ();
      
      if ( !$list || ref $list ne 'ARRAY' || !scalar @{$list} ) {
        $res{text} = qq{ "$label : $lang{NO_DATA}" };
      }
      else {
        if ( $attr->{has_next} ) {
          $attr->{next} = { load => 1 };
        }
        
        %res = (select => to_json({
            name    => $name,
            label   => $label,
            options => $sub_make_select_options->($list, $attr->{name_key} || 'name', $attr->{id_key} || 'id'),
            %{ $attr ? $attr : {} }
          })
        )
      }
      
      push @{$html->{JSON_OUTPUT}}, \%res;
      1;
    };
    
    #*****************************************************
    # input_next_option - return JSON input field to extend search form
    #
    #*****************************************************
    my $input_next_option = sub {
      my ($name, $label, $attr) = @_;
      
      my %res = (
        input => to_json({
          name  => $name,
          label => $label,
          %{ $attr ? $attr : {} }
        })
      );
      
      push @{$html->{JSON_OUTPUT}}, \%res;
      1;
    };
    
    my $type = $CONNECTION_TYPES{$FORM{TYPE}};
    
    if ( $type eq 'fiber' ) {
      
      if ( !$FORM{TO_COMMUTATION} ) {
        # Get all commutations where this cable is involved
        my $commutations_for_cable_list = $Cablecat->commutations_list({
          ID        => '_SHOW',
          CABLE_IDS => $cable_id,
          PAGE_ROWS => 10000
        });
        _error_show($Cablecat);
        
        my @commutations = map {
          {
            name => "$lang{COMMUTATION}#$_->{id}",
            id   => $_->{id}
          }
        } @{$commutations_for_cable_list};
        
        return $select_next_option->('TO_COMMUTATION', $lang{COMMUTATION}, \@commutations, { has_next => 1 });
      }
      # Cable
      if ( !$FORM{TO_CABLE_ID} ) {
        
        my $commutations = $Cablecat->commutations_list({
          ID        => $FORM{TO_COMMUTATION},
          CABLE_IDS => '_SHOW',
          CABLES    => '_SHOW',
          PAGE_ROWS => 10000
        });
        _error_show($Cablecat);
        
        my %unique_cables = ();
        foreach ( @{$commutations} ) {
          my @cable_ids = split(',\s?', $_->{cable_ids});
          my @cable_names = split(',\s?', $_->{cables});
          
          for( 0 .. $#cable_ids ) {
            $unique_cables{$cable_ids[$_]} = $cable_names[$_];
          }
        }
        # Delete this cable from list
        delete $unique_cables{$cable_id};
        
        my @unique_list = map {
          {
            id   => $_,
            name => $unique_cables{$_}
          }
        } sort keys (%unique_cables);
        
        return $select_next_option->('TO_CABLE_ID', $lang{CABLE}, \@unique_list, { has_next => 1 });
      }
      elsif ( !$FORM{TO_FIBER} ) {
        my $cable_id_to_connect = $FORM{TO_CABLE_ID};
        
        my $cable_info = $Cablecat->cables_info($cable_id_to_connect, {
            FIBERS_COUNT     => '_SHOW',
            SHOW_ALL_COLUMNS => 0,
            COLS_UPPER       => 0
          });
        
        my $fibers_list = [ map {{ id => $_, name => $_ }} (1 .. $cable_info->{fibers_count}) ];
        
        return $select_next_option->('TO_FIBER', "$lang{FIBER} #", $fibers_list);
      }
    }
    elsif ( $type eq 'equipment' ) {
      
      # Equipment_select
      if ( !$FORM{EQUIPMENT_ID} ) {
        my $equipment_list = $Equipment->_list({ COLS_NAME => 1, NAS_NAME => '_SHOW', PAGE_ROWS => 10000 });
        
        return $select_next_option->('EQUIPMENT_ID', $lang{EQUIPMENT}, $equipment_list, {
            name_key => 'nas_name',
            id_key   => 'nas_id',
            has_next => 1
          });
      }
      elsif ( $FORM{EQUIPMENT_ID} ) {
        
        my $equipment_info = $Equipment->_info($FORM{EQUIPMENT_ID});
        
        my $ports_count = $equipment_info->{PORTS};
        
        my @options_list = map {
          { id => $_, name => $_ }
        } (1 ... $ports_count);
        
        return $select_next_option->('EQUIPMENT_PORT', $lang{PORT}, \@options_list);
      }
    }
    elsif ( $type eq 'point_id' ) {
      if ( !$FORM{POINT_TYPE_ID} ) {
        my $point_types_list = $Maps->point_types_list({
          ID         => '_SHOW',
          NAME       => '_SHOW',
          PAGE_ROWS  => 10000,
          COLS_UPPER => 0
        });
        _error_show($Maps);
        
        return $select_next_option->('POINT_TYPE_ID', $lang{BY_TYPE}, translate_list($point_types_list),
          { has_next => 1 });
      }
      # point select
      elsif ( !$FORM{POINT_ID} ) {
        my $points_for_type_list = $Maps->points_list({
          ID         => '_SHOW',
          NAME       => '_SHOW',
          TYPE_ID    => $FORM{POINT_TYPE_ID},
          PAGE_ROWS  => 10000,
          COLS_UPPER => 0
        });
        _error_show($Maps);
        
        # Port select
        return $select_next_option->('POINT_ID', $lang{OBJECT}, $points_for_type_list);
      }
    }
    elsif ( $type eq 'splitter' ) {
      if ( !$FORM{WELL_ID} ) {
        my $wells_list = $Cablecat->wells_list({
          ID         => '_SHOW',
          NAME       => '_SHOW',
          COLS_UPPER => 0,
          PAGE_ROWS  => 10000
        });
        _error_show($Cablecat);
        
        # connecter select
        return $select_next_option->('WELL_ID', $lang{WELL}, $wells_list, { has_next => 1 });
      }
      elsif ( !$FORM{SPLITTER_ID} ) {
        my $splitters_list = $Cablecat->splitters_list({
          ID         => '_SHOW',
          TYPE       => '_SHOW',
          WELL_ID    => $FORM{WELL_ID},
          COLS_UPPER => 0,
          PAGE_ROWS  => 10000
        });
        _error_show($Cablecat);
        
        # Generating name
        @{$splitters_list} = map {$_->{name} = $_->{type} . '_' . $_->{id};
          $_} @{$splitters_list};
        
        # connecter select
        return $select_next_option->('SPLITTER_ID', $lang{SPLITTER}, $splitters_list, { has_next => 1 });
      }
      if ( !$FORM{SPLITTER_DIRECTION} ) {
        
        my @splitter_directions = (
          { name => $lang{ENTER}, id => 1 },
          { name => $lang{OUTER}, id => 2 }
        );
        
        # splitter direction select
        return $select_next_option->('SPLITTER_DIRECTION', $lang{DIRECTION}, \@splitter_directions, { has_next => 1 });
      }
      if ( !$FORM{SPLITTER_PORT} ) {
        my $splitter_info = $Cablecat->splitters_info($FORM{SPLITTER_ID});
        
        my $max_number = $splitter_info->{ $FORM{SPLITTER_DIRECTION} == 1 ? 'fibers_in' : 'fibers_out' };
        
        my @options = map {
          { id => $_, name => $_ }
        } (1 ... $max_number);
        
        return $select_next_option->('SPLITTER_PORT', $lang{PORT}, \@options);
      }
    }
    elsif ( $type eq 'client' ) {
      my %search_types = (
        1 => 'ADDRESS',
        2 => 'TAGS',
        3 => 'GROUP'
      );
      
      if ( !in_array('Tags', \@MODULES) ) {
        delete $search_types{2};
      }
      
      my %SEARCH_PARAMS = ();
      
      if ( !$FORM{SEARCH_BY} ) {
        
        my @search_options = map {
          {
            id   => $_,
            name => _translate('$lang{' . $search_types{$_} . '}')
          }
        } sort keys %search_types;
        
        # search type select
        return $select_next_option->('SEARCH_BY', $lang{SEARCH}, \@search_options, { has_next => 1 });
      }
      elsif ( $FORM{SEARCH_BY} == 1 ) {
        # Address
        require Address;
        my $Address = Address->new($db, $admin, \%conf);
        
        # District select
        if ( !$FORM{DISTRICT_ID} ) {
          my $districts_list = $Address->district_list({
            COLS_NAME  => 1,
            COLS_UPPER => 0,
            PAGE_ROWS  => 10000
          });
          
          return $select_next_option->('DISTRICT_ID', $lang{DISTRICT}, $districts_list, { has_next => 1 });
        }
        elsif ( !$FORM{STREET_ID} ) {
          my $streets_list = $Address->street_list({
            DISTRICT_ID => $FORM{DISTRICT_ID},
            STREET_NAME => '_SHOW',
            COLS_NAME   => 1,
            COLS_UPPER  => 0,
            PAGE_ROWS   => 10000
          });
          
          return $select_next_option->('STREET_ID', $lang{STREET}, $streets_list, {
              name_key => 'street_name',
              id_key   => 'street_id',
              has_next => 1
            });
        }
        elsif ( !$FORM{LOCATION_ID} ) {
          my $builds_list = $Address->build_list({
            STREET_ID  => $FORM{STREET_ID},
            COLS_NAME  => 1,
            COLS_UPPER => 0,
            PAGE_ROWS  => 10000
          });
          
          return $select_next_option->('LOCATION_ID', $lang{BUILD}, $builds_list,
            { name_key => 'number', has_next => 1 });
        }
        else {
          $SEARCH_PARAMS{LOCATION_ID} = $FORM{LOCATION_ID};
        }
      }
      elsif ( $FORM{SEARCH_BY} == 2 ) {
        # Tags
        require Tags;
        my $Tags = Tags->new($db, $admin, \%conf);
        if ( !$FORM{TAG_ID} ) {
          
          # Tag select
          my $tags_list = $Tags->list({
            NAME       => '_SHOW',
            ID         => '_SHOW',
            COLS_NAME  => 1,
            COLS_UPPER => 0,
            PAGE_ROWS  => 10000
          });
          
          return $select_next_option->('TAG_ID', $lang{TAGS}, $tags_list, { has_next => 1 });
        }
        
        # Tags uses his own user search logic
        my $users_list = $Tags->tags_list({
          TAG_ID    => $FORM{TAG_ID},
          LOGIN     => '_SHOW',
          COLS_NAME => 1,
          PAGE_ROWS => 10000,
        });
        
        return $select_next_option->('UID', $lang{USER}, $users_list, {
            name_key => 'login',
            id_key   => 'uid'
          });
      }
      elsif ( $FORM{SEARCH_BY} == 3 ) {# Group
        if ( !$FORM{GROUP_ID} ) {
          my $groups_list = $users->groups_list({
            NAME      => '_SHOW',
            ID        => '_SHOW',
            COLS_NAME => 1,
            PAGE_ROWS => 10000
          });
          
          # Group select
          return $select_next_option->('GROUP_ID', $lang{GROUP}, $groups_list, {
              id_key   => 'gid',
              has_next => 1
            });
        }
        else {
          $SEARCH_PARAMS{GID} = $FORM{GROUP_ID};
        }
      }
      
      # Search by selected type
      
      my $users_list = $users->list({
        UID       => '_SHOW',
        LOGIN     => '_SHOW',
        %SEARCH_PARAMS,
        COLS_NAME => 1,
        PAGE_ROWS => 10000
      });
      
      return $select_next_option->('UID', $lang{USER}, $users_list, {
          name_key => 'login',
          id_key   => 'uid'
        });
    }
    elsif ( $type eq 'cross' ) {
      if ( !$FORM{WELL_ID} ) {
        my $wells_list = $Cablecat->wells_list({
          ID         => '_SHOW',
          NAME       => '_SHOW',
          COLS_UPPER => 0,
          PAGE_ROWS  => 10000
        });
        _error_show($Cablecat);
        
        # connecter select
        return $select_next_option->('WELL_ID', $lang{WELL}, $wells_list, { has_next => 1 });
      }
      elsif ( !$FORM{CROSS_ID} ) {
        my $crosses_list = $Cablecat->crosses_list({
          ID         => '_SHOW',
          NAME       => '_SHOW',
          WELL_ID    => $FORM{WELL_ID},
          COLS_UPPER => 0,
          PAGE_ROWS  => 10000
        });
        _error_show($Cablecat);
        
        # connecter select
        return $select_next_option->('CROSS_ID', $lang{CROSS}, $crosses_list, { has_next => 1 });
      }
      elsif ( !$FORM{CROSS_PORT} ) {
        my $selected_cross = $Cablecat->crosses_info($FORM{CROSS_ID}, { COLS_UPPER => 0 });
        my $ports_count = $selected_cross->{ports_count};
        my @ports_list = map {{ id => $_, name => $_ }} (1 .. $ports_count);
        
        #        _bp('', \@ports_list);
        
        return $select_next_option->('CROSS_PORT', $lang{PORT}, \@ports_list);
      }
    }
  }
  
  push @{$html->{JSON_OUPUT}},
    {
      text => $lang{ERROR}
    };
  
  return 1;
}

#**********************************************************
=head2 cablecat_cable_links_table()

=cut
#**********************************************************
sub cablecat_cable_links_table {
  my ($cable_id) = @_;
  
  my %cable = ();
  # Allow to pass DB row
  if ( !ref $cable_id ) {
    my $cable_info = $Cablecat->cables_info($cable_id);
    return 0 if ( _error_show($Cablecat) );
    
    %cable = %{$cable_info};
  }
  else {
    %cable = %{$cable_id};
    $cable_id = $cable{id};
  }
  
  return '' unless ( $cable{fibers_count} && $cable{modules_count} );
  my $fibers_count = $cable{fibers_count};
  my $modules_count = $cable{modules_count};
  my $fibers_in_module = int($fibers_count / $modules_count);
  
  return '' unless ( $cable{fibers_colors} && $cable{modules_colors} );
  my @colors = split(',', $cable{fibers_colors});
  my @module_colors = split(',', $cable{modules_colors});
  
  # Get commutations for this cable
  my $commutations = $Cablecat->commutations_list({
    CABLE_IDS        => $cable_id,
    SHOW_ALL_COLUMNS => 1,
    PAGE_ROWS        => 10000
  });
  
  return '' if ( _error_show($Cablecat) || !scalar(@{$commutations}) );
  
  my @commutation_ids = map {$_->{id}} @{$commutations};
  
  my $links = $Cablecat->commutation_links_list({
    COMMUTATION_ID   => join(';', @commutation_ids),
    SHOW_ALL_COLUMNS => 1,
    PAGE_ROWS        => 10000
  });
  return 0 if ( _error_show($Cablecat) );
  
  # Links where this cable is cable_id_2
  my @this_cable_left_links = grep {$_->{cable_id_1} == $cable_id} @{$links};
  my @this_cable_right_links = grep {$_->{cable_id_2} == $cable_id} @{$links};
  
  my $fibers_for_left = sort_array_to_hash(\@this_cable_left_links, 'fiber_num_1');
  my $fibers_for_right = sort_array_to_hash(\@this_cable_right_links, 'fiber_num_2');
  
  my $links_info = $Cablecat->links_for_element_list('CABLE', $cable_id, {
      SHOW_ALL_COLUMNS => 1,
      PAGE_ROWS        => 10000
    });
  return 0 if ( _error_show($Cablecat) );
  
  # Maybe: think about refactoring to one sub with direction as argument
  
  my @links_for_left = grep {!$_->{element_1_side}} @{$links_info};
  my @links_for_right = grep {defined $_->{element_1_side} && $_->{element_1_side} == 1} @{$links_info};
  
  my $left_link_info = sort_array_to_hash(\@links_for_left, 'fiber_num');
  my $right_link_info = sort_array_to_hash(\@links_for_right, 'fiber_num');
  
  my $sub_get_info_for_fiber = sub {
    my ($f_num, $is_right) = @_;
    my $res = '';
    
    my $info_hash = ($is_right) ? $fibers_for_right : $fibers_for_left;
    my $links_hash = ($is_right) ? $right_link_info : $left_link_info;
    
    my $pos_id = ($is_right) ? 1 : 2;
    
    my $is_fiber_link = 0;
    if ( exists $info_hash->{$f_num} && defined $info_hash->{$f_num}->{commutation_id} ) {
      my $info = $info_hash->{$f_num};
      $is_fiber_link = 1;
      
      my $commutation_btn = $html->button(
        $lang{COMMUTATION} . '#' . $info->{commutation_id},
        'get_index=cablecat_commutation&full=1&ID=' . $info->{commutation_id}
      );
      my $cable_btn = _cablecat_get_cable_link($info->{'cable_id_' . $pos_id});
      my $br = $html->br;
      
      $res .= "$cable_btn : $lang{FIBER}# " . $html->b($info->{'fiber_num_' . $pos_id}) . $br;
      $res .= "$commutation_btn $br";
      
      $res .= ($info->{attenuation})
        ? "$lang{ATTENUATION} :  $info->{attenuation} $br"
        : '';
      
      $res .= ($info->{comments})
        ? "$lang{COMMENTS} :  $info->{comments} $br"
        : '';
    }
    elsif ( exists $links_hash->{$f_num} ) {
      $res = _cablecat_external_link_info($cable_id, $f_num, $pos_id == 2, $links_hash->{$f_num});
      
      return _cablecat_link_info_cell($cable_id, $f_num, $pos_id == 2, $res);
    }
    else {
      return $html->button('', '', {
          class     => 'add',
          ICON      => 'glyphicon glyphicon-plus',
          BUTTON    => 1,
          SKIP_HREF => 1,
          ex_params => "onClick='addLinkInfo($cable_id, $f_num, $is_right)'"
        })
    }
    return $res;
  };
  
  # Table will consist of two columns, and $fibers_num rows
  my @rows = ();
  foreach my $fiber_num ( 0 .. $fibers_count - 1 ) {
    my $module_num = int($fiber_num / $fibers_in_module);
    
    my $fiber_color = $colors[$fiber_num % $fibers_in_module];
    my $module_color = $module_colors[ $module_num ];
    #
    my $info_left = $sub_get_info_for_fiber->(int ($fiber_num + 1), 0);
    my $info_right = $sub_get_info_for_fiber->(int ($fiber_num + 1), 1);
    
    push (@rows,
      [
        $html->element('div', '&nbsp;', {
            style => 'background-color : #' . $module_color,
            class => 'colored-block module-color'
          })
          . $html->element('div', '&nbsp;', {
            style => 'background-color : #' . $fiber_color,
            class => 'colored-block fiber-color'
          })
          . ($module_num + 1) . ':' . ($fiber_num + 1),
        $info_left, $info_right
      ]
    );
  }
  
  my $table = $html->table({
    caption     => "$lang{INFO} : $cable{name}",
    #    HIDE_TABLE => 1,
    #    EXPORT => 1,
    title_plain => [ "$lang{MODULE}:$lang{FIBER}", $lang{ENTER}, $lang{OUTER} ],
    ID          => 'CABLECAT_CABLE_LINKS_ID',
    rows        => \@rows
  });
  
  # SHOULD BE THE SAME IN cablecat_link_info
  my %connection_type_lang = (
    1 => 'FIBER',
    2 => 'PORT',
    3 => 'OBJECT',
    4 => 'SPLITTER',
    5 => 'USER',
    6 => 'CROSS',
  );
  
  my @connection_type_modal_select_options = ({ name => '--', value => '--' }, map {
      {
        name  => _translate('$lang{' . $connection_type_lang{$_} . '}'),
        value => $_
      };
    } sort keys %CONNECTION_TYPES
  );
  
  $html->tpl_show(_include('cablecat_cable_links_info', 'Cablecat'),
    {
      LINK_TYPE_OPTIONS => to_json(\@connection_type_modal_select_options),
      CABLE_ID          => $cable_id
    }
  );
  
  return $table->show();
}


#**********************************************************
=head2 _cablecat_break_cable_in_two_parts()

=cut
#**********************************************************
sub _cablecat_break_cable_in_two_parts {
  my ($break_cable_id, $inserted_well_id) = @_;
  return unless ( $break_cable_id );
  
  # Insert well, breaks cable
  my $add_result = $Cablecat->break_cable($break_cable_id, $inserted_well_id);
  if ( !$add_result || ref $add_result ne 'ARRAY' ) {
    _error_show($Cablecat);
    return qq{Can\'t insert well : $add_result };
  };
  
  my ( $deleted_cable, $cable_id_1, $cable_id_2 ) = @{$add_result};
  
  # Should also break polyline for this cables
  my $break_result = $Maps->break_polyline(
    # REVERSE coords
    $deleted_cable->{point_id},
    {
      COORDX => $FORM{COORDX},
      COORDY => $FORM{COORDY}
    }
  );
  
  if ( !$break_result || ref $break_result ne 'ARRAY' ) {
    _error_show($Maps);
    return qq{Can\'t break polyline : $break_result }
  };
  
  my ($new_object1_id, $new_object2_id ) = @{$break_result};
  
  # Update cables info
  #FIXME: check object adding order
  $Cablecat->cables_change({ ID => $cable_id_1, POINT_ID => $new_object2_id });
  $Cablecat->cables_change({ ID => $cable_id_2, POINT_ID => $new_object1_id });
  
  return 1;
}


#**********************************************************
=head2 _cablecat_commutation_cables_prepare_json($cable_id) - get cable info, and return JSON string ref

  Arguments:
    $cable_id - int, Cablecat cable id
    
  Returns:
    hash_ref - JSON like structure for cable

=cut
#**********************************************************
sub _cablecat_commutation_cables_prepare_json {
  my ($cable_id, $attr) = @_;
  $cable_id =~ s/,/;/g if ( $cable_id );
  
  my $cables_list = [];
  
  if ( $attr->{COMMUTATION_ID} ) {
    # Return info for all cables on commutation
    my $commutation_info = $Cablecat->commutations_info($attr->{COMMUTATION_ID}, {
        SHOW_ALL_COLUMNS => 0,
        CABLE_IDS        => '_SHOW',
        COLS_UPPER       => 0
      });
    $cable_id = $commutation_info->{cable_ids};
    $cable_id =~ s/,/;/g if ( $cable_id );
    
    $cables_list = $Cablecat->cables_list({
      ID               => $cable_id || '_SHOW',
      COMMUTATION_ID   => $attr->{COMMUTATION_ID},
      SHOW_ALL_COLUMNS => 1,
      PAGE_ROWS        => 10000,
      COLS_UPPER       => 0,
    });
    _error_show($Cablecat);
    
  }
  elsif ( $cable_id ) {
    # Return info for cable ( cables ) specified by $cable_id
    $cables_list = $Cablecat->cables_list({
      ID               => $cable_id,
      SHOW_ALL_COLUMNS => 1,
      PAGE_ROWS        => 1,
      COLS_UPPER       => 0,
    });
    _error_show($Cablecat);
  }
  else {
    return '';
  }
  
  my @result_list = ();
  foreach my $cable ( @{$cables_list} ) {
    
    if ( !defined $cable->{modules_count} || !defined $cable->{fibers_count} ) {
      my $cable_link = _cablecat_get_cable_link($cable->{id});
      $html->message('warn', $lang{ERROR},
        "Modules or fibers count is not defined. Can't display cable $cable_link ($cable->{id})");
      return 0;
    }
    _error_show($Cablecat);
    
    if ( !$cable->{modules_colors} || !$cable->{fibers_colors} ) {
      $html->message('err', $lang{ERROR}, "$lang{NO} $lang{COLOR_SCHEME} $lang{FOR} "
          . _cablecat_get_cable_link($cable->{id}, { NAME => $cable->{name} })
          . " ( $cable->{id} )"
      );
      return 0;
    }
    
    my $other_commutations_for_cable = $Cablecat->commutation_cables_list({
      CABLE_ID       => $cable->{id},
      COMMUTATION_ID => ($attr->{COMMUTATION_ID}) ? ('!' . $attr->{COMMUTATION_ID}) : '_SHOW',
      CONNECTER      => '_SHOW',
    });
    
    my %other_commutation_hash = map {
      $_->{commutation_id} => $_->{connecter}
    } @{$other_commutations_for_cable};
    
    $cable->{outer_color} //= '#000000';
    push @result_list, {
        id    => + $cable->{id},
        image => {
          modules              => + $cable->{modules_count},
          fibers               => + $cable->{fibers_count},
          color                => $cable->{outer_color},
          color_scheme         => [ map {'#' . $_} split ',', $cable->{fibers_colors} ],
          modules_color_scheme => [ map {'#' . $_} split ',', $cable->{modules_colors} ],
        },
        meta  => {
          name               => $cable->{name},
          #      position => $position,
          #      fibers   => \%fibers,
          well_1_id          => $cable->{well_1_id},
          well_2_id          => $cable->{well_2_id},
          well_1             => $cable->{well_1},
          well_2             => $cable->{well_2},
          map_btn            => maps_show_object_button(
            $MAP_LAYER_ID{CABLE},
            $cable->{id}, {
              GO_TO_MAP   => 1,
              POINT_ID    => $cable->{point_id},
              SINGLE      => $cable->{point_id},
              RETURN_HREF => 1
            }
          ),
          other_commutations => \%other_commutation_hash
        }
      };
  }
  
  return \@result_list;
}

#**********************************************************
=head2 _cablecat_commutation_splitters($splitter_id, $attr)
  
  $splitter_id - num or array_ref
  
=cut
#**********************************************************
sub _cablecat_commutation_splitters {
  my ($splitter_id, $attr) = @_;
  
  my $splitters_list = [];
  if ( $attr->{COMMUTATION_ID} ) {
    $splitters_list = $Cablecat->splitters_list({
      ID               => $splitter_id || '_SHOW',
      COMMUTATION_ID   => $attr->{COMMUTATION_ID},
      SHOW_ALL_COLUMNS => 1,
      PAGE_ROWS        => 10000,
      COLS_UPPER       => 0,
    });
    _error_show($Cablecat);
  }
  elsif ( $splitter_id ) {
    $splitters_list = $Cablecat->splitters_list({
      ID               => $splitter_id,
      SHOW_ALL_COLUMNS => 1,
      PAGE_ROWS        => 1,
      COLS_UPPER       => 0,
    });
    _error_show($Cablecat);
  }
  else {
    return '';
  }
  
  #  my @result_list = ();
  #  foreach my $splitter ( $splitters_list ) {
  #
  #  }
  #
  #  return \@result_list;
  return $splitters_list;
}

#**********************************************************
=head2 _cablecat_commutation_equipment($splitter_id)

=cut
#**********************************************************
sub _cablecat_commutation_equipment {
  my ($equipment_id, $attr) = @_;
  
  my $equipment_list = [];
  if ( $attr->{COMMUTATION_ID} ) {
    $equipment_list = $Cablecat->commutation_equipment_list({
      ID               => $equipment_id || '_SHOW',
      COMMUTATION_ID   => $attr->{COMMUTATION_ID},
      SHOW_ALL_COLUMNS => 1,
      PAGE_ROWS        => 10000,
      COLS_UPPER       => 0,
    });
    _error_show($Cablecat);
  }
  elsif ( $equipment_id ) {
    $equipment_list = $Cablecat->commutation_equipment_list({
      ID               => $equipment_id,
      SHOW_ALL_COLUMNS => 1,
      PAGE_ROWS        => 1,
      COLS_UPPER       => 0,
    });
    _error_show($Cablecat);
  }
  else {
    return '';
  }
  
  return [
    map {
      $_->{commutation_equipment_id} = $_->{id};
      $_->{id} = $_->{nas_id} if ( $_->{nas_id} );
      $_
    } @{$equipment_list}
  ];
}

#**********************************************************
=head2 _cablecat_external_link_info($cable_id, $fiber_num, $direction, $info)

=cut
#**********************************************************
sub _cablecat_external_link_info {
  my ($cable_id, $fiber_num, $direction, $info) = @_;
  
  if ( !$info ) {
    
    # Search former will not include 'false' direction in query unless it's a string
    $direction = '0' if ( !$direction );
    my $list = $Cablecat->links_list({
      CABLE_ID         => $cable_id,
      FIBER_NUM        => $fiber_num,
      DIRECTION        => $direction,
      SHOW_ALL_COLUMNS => 1
    });
    _error_show($Cablecat);
    
    $info = $list->[0];
  }
  
  if ( $info->{uid} ) {
    my $user_info = $users->info($info->{uid});
    return user_ext_menu($user_info->{UID}, $user_info->{LOGIN});
  }
  elsif ( $info->{equipment_id} ) {
    
    my $equipment_info = $Equipment->_info($info->{equipment_id});
    
    return $html->b($equipment_info->{SYSTEM_ID} || '') . ' : ' . ($info->{equipment_port} || q{});
  }
  elsif ( $info->{splitter_id} ) {
    my $splitter = $Cablecat->splitters_info($info->{splitter_id});
    return "$lang{SPLITTER} " . $html->b("$splitter->{TYPE}#$splitter->{ID}") . ' : ' . ($info->{splitter_port} || q{});
  }
  elsif ( $info->{point_id} ) {
    my $object = $Maps->points_info($info->{point_id});
    my $type = _translate($object->{TYPE});
    
    return "$lang{OBJECT} : $object->{NAME}($lang{TYPE} : $type)";
  }
  elsif ( $info->{cross_id} ) {
    my $cross = $Cablecat->crosses_info($info->{cross_id}, {
        COLS_UPPER       => 0,
        SHOW_ALL_COLUMNS => 0,
        NAME             => '_SHOW',
      });
    
    my $cross_link = function_button($cross->{name}, 'cablecat_crosses', $cross->{id});
    return "$lang{CROSS} : $cross_link#$info->{cross_port}";
  }
  
  return '';
}

#**********************************************************
=head2 _cablecat_link_info_cell($cable_id, $fiber_num, $direction, $html)

=cut
#**********************************************************
sub _cablecat_link_info_cell {
  my ($cable_id, $fiber_num, $direction, $data) = @_;
  
  $direction ||= 0;
  
  if ( !$data ) {
    return $html->button('', '', {
        class     => 'add',
        ICON      => 'glyphicon glyphicon-plus',
        BUTTON    => 1,
        SKIP_HREF => 1,
        ex_params => "onClick='addLinkInfo($cable_id, $fiber_num, $direction)'"
      })
  }
  
  my $del_btn = $html->button('', '', {
      class     => 'btn btn-xs btn-danger',
      ICON      => 'glyphicon glyphicon-remove',
      SKIP_HREF => 1,
      ex_params => "onClick='clearLinkInfo($cable_id, $fiber_num, $direction)'"
    });
  
  return $html->element('div', $data, { class => 'col-xs-10' })
    . $html->element('div', $del_btn, { class => 'col-xs-2' });
  
}


#**********************************************************
=head2 _cablecat_get_cable_link()

=cut
#**********************************************************
sub _cablecat_get_cable_link {
  my ($cable_id, $attr) = @_;
  state $cables_info = {};
  
  return '' unless ( $cable_id );
  
  if ( !exists $cables_info->{$cable_id} ) {
    
    if ( $attr->{NAME} ) {
      $cables_info->{$cable_id}->{name} = $attr->{NAME};
    }
    else {
      $cables_info->{$cable_id} = $Cablecat->cables_info($cable_id);
      _error_show($Cablecat);
    }
    
  }
  
  my $info = $cables_info->{$cable_id};
  
  return $html->button($info->{name}, 'get_index=cablecat_cables&full=1&chg=' . $cable_id);
}

#**********************************************************
=head2 _cablecat_address_for_well_id()

=cut
#**********************************************************
sub _cablecat_address_for_well_id {
  my ($well_id) = @_;
  
  my $well_info = $Cablecat->wells_info($well_id);
  if ( $well_info->{point_id} ) {
    my $point_info = $Maps->points_info($well_info->{point_id}, { ADDRESS_FULL => '_SHOW', COLS_UPPER => 0 });
    
    my $map_btn = _cablecat_result_former_point_id_filter($well_info->{point_id},
      { PARAMS => [ $MAP_LAYER_ID{WELL} ] });
    my $address = $point_info->{address_full} || '';
    
    return  $address . ' ' . $map_btn;
  }
  
  #TODO: show 'edit/set' button
  
  return '';
}

#**********************************************************
=head2 _cablecat_cable_length($cable_id)

  Arguments:
    $cable_id,
    
  Returns:
     length in meters
=cut
#**********************************************************
sub _cablecat_cable_length {
  
  my ($cable_id) = @_;
  
  my $cable = $Cablecat->cables_info($cable_id);
  my $polyline_id = $cable->{polyline_id};
  return 0 unless ( $cable->{polyline_id} );
  
  my $points_list = $Maps->polyline_points_list({
    POLYLINE_ID => $polyline_id,
    REVERSE     => 1,
    COORDX      => '_SHOW',
    COORDY      => '_SHOW',
  });
  _error_show($Maps);
  
  my @points = @{$points_list};
  
  my $i = 0;
  my $cable_length = 0;
  while ( $i++ < $#points ) {
    $cable_length += Maps::mercator_to_meters($points[$i]->{COORDX}, $points[$i]->{COORDY},
      $points[$i - 1]->{COORDX}, $points[$i - 1]->{COORDY});
  }
  
  return $cable_length;
}
1;
