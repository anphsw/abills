#!perl

=head1 NAME

  Portal webinterface

=cut

use strict;
use warnings FATAL => 'all';

use Abills::Api::Handle;
use Abills::Base qw(in_array convert is_html);

use Portal::Constants qw(ALLOWED_METHODS);

our (
  $db,
  $admin,
  %conf,
  %lang,
  $html,
  %permissions,
  @REGISTRATION,
  $users
);

my $Api = Abills::Api::Handle->new($db, $admin, \%conf, {
  html    => $html,
  lang    => \%lang,
  cookies => \%COOKIES,
  direct  => 1
});

my @allowed_methods = ('Telegram', 'Viber_bot', 'Push');

#***********************************************************
=head2 portal_manage_menu() -  Portal manage menu

=cut
#***********************************************************
sub portal_manage_menu {
  my %web_params = ();
  $web_params{ACTION}     = 'add';
  $web_params{ACTION_LNG} = $lang{ADD};
  $web_params{TITLE_NAME} = $lang{ADD_MENU};

  if (!$FORM{STATUS} or $FORM{STATUS} == 0) {
    $web_params{SHOWED} = '';
    $web_params{HIDDEN} = 'checked=\'checked\'';
  }
  else {
    $web_params{SHOWED} = 'checked=\'checked\'';
    $web_params{HIDDEN} = '';
  }

  if ($FORM{add}) {
    my ($res) = $Api->api_call({
      METHOD => 'POST',
      PATH   => '/portal/menus',
      PARAMS => \%FORM
    });

    if (!_error_show($res)) {
      $html->message('info', $lang{ADDED});
    }
  }
  elsif ($FORM{del}) {
    my ($res) = $Api->api_call({
      METHOD => 'DELETE',
      PATH   => "/portal/menus/$FORM{del}",
    });

    if (!_error_show($res)) {
      $html->message('info', $lang{INFO}, "$lang{DELETED}");
    }
  }
  elsif ($FORM{change} && $FORM{id}) {
    $web_params{ACTION}     = 'change';
    $web_params{ACTION_LNG} = $lang{CHANGE};
    $web_params{TITLE_NAME} = $lang{CHANGE_MENU};

    my ($res) = $Api->api_call({
      METHOD => 'PUT',
      PATH => "/portal/menus/$FORM{id}",
      PARAMS => \%FORM
    });

    if (!_error_show($res)) {
      $html->message('info', $lang{CHANGED});
    }
  }
  elsif ($FORM{chg}) {
    $web_params{ACTION}     = 'change';
    $web_params{ACTION_LNG} = $lang{CHANGE};
    $web_params{TITLE_NAME} = $lang{CHANGE_MENU};

    my ($menu_info) = $Api->api_call({
      PATH => "/portal/menus/$FORM{chg}"
    });

    if ($menu_info->{status}) {
      $web_params{SHOWED} = 'checked';
    }
    %web_params = (%web_params, %$menu_info);
  }

  $html->tpl_show(_include('portal_menu_add', 'Portal'), { %web_params, %FORM });

  my ($menu_res) = $Api->api_call({
    PATH   => '/portal/menus',
    PARAMS => \%LIST_PARAMS
  });

  result_former({
    DATAHASH => $menu_res->{list},
    DATATOTAL => $menu_res->{total},
    DEFAULT_FIELDS  => 'id,name,url,date,status',
    FUNCTION_FIELDS => 'change,del',
    SKIP_USER_TITLE => 1,
    EXT_TITLES      => {
      id     => 'ID',
      name   => $lang{NAME},
      url    => 'URL',
      date   => $lang{ADDED},
      status => $lang{STATUS}
    },
    TABLE           => {
      width      => '100%',
      caption    => "$lang{MENU}",
      qs         => $pages_qs,
      ID         => 'PORTAL_TYPES',
      EXPORT     => 1,
      SHOW_COLS  => 0,
      MENU       => "$lang{ADD}:index=$index&add_form=1&$pages_qs:add",
    },
    SELECT_VALUE => {
      status => {
        0 => "$lang{HIDDEN}:text-danger",
        1 => "$lang{SHOWED}:text-primary"
      },
    },
    MAKE_ROWS     => 1,
    TOTAL         => 1
  });

  return 1;
}

# TODO: rework this unoptimal function
#***********************************************************
=head2 portal_articles()-  Portal articles

=cut
#***********************************************************
sub portal_articles {
  require Abills::Sender::Core;
  my $Sender = Abills::Sender::Core->new($db, $admin, \%conf);

  my %web_params = ();
  $web_params{ACTION}     = 'add';
  $web_params{ACTION_LNG} = $lang{ADD};
  $web_params{TITLE_NAME} = $lang{ADD_ARTICLE};

  my @IMPORTANCE_STATUS = ("$lang{NORMAL}","$lang{CRITICAL}");

  my $importance_select = $html->form_select('IMPORTANCE', {
    SELECTED     => $FORM{importance},
    SEL_ARRAY    => \@IMPORTANCE_STATUS,
    ARRAY_NUM_ID => 1,
    SEL_VALUE    => 'name',
    NO_ID        => 1
  });

  my $tags_list;
  my $Tags;
  if (in_array('Tags', \@MODULES)) {
    require Tags;
    Tags->import();
    $Tags = Tags->new($db, $admin, \%conf);
    $tags_list = $Tags->list({COLS_NAME => 1, NAME => '_SHOW'});

    $web_params{TAGS} = $html->form_select('TAGS', {
      SEL_LIST     => $tags_list,
      SEL_KEY      => 'id',
      SEL_VALUE    => 'name',
      NO_ID        => 1,
      SEL_OPTIONS  => {"" => ""}
    });
  }
  else {
    $web_params{TAGS} = "Tags $lang{DISABLED}";
  }

  my $domains_list;
  my $Multidoms;
  if (in_array('Multidoms', \@MODULES)) {
    load_module('Multidoms');
    $Multidoms = Multidoms->new($db, $admin, \%conf);
    $domains_list = $Multidoms->multidoms_domains_list({
      COLS_NAME => 1,
      NAME      => '_SHOW'
    });

    $web_params{DOMAIN_ID} = $html->form_select('DOMAIN_ID', {
      SELECTED     => $web_params{DOMAIN_ID},
      SEL_LIST     => $domains_list,
      SEL_KEY      => 'id',
      SEL_VALUE    => 'name',
      NO_ID        => 1,
      SEL_OPTIONS  => {"" => ""}
    });
  }
  else {
    $web_params{DOMAIN_ID} = "Multidoms $lang{DISABLED}";
    # very dumb
    # style if domains not exist
    $web_params{DOMAIN_STYLE} = 'display: none';
  }

  $web_params{GROUPS} = sel_groups({
    FILTER_SEL => 1
  });
  $web_params{IMPORTANCE_STATUS} = $importance_select;
  $web_params{ADDRESS_FORM} = form_address_select({
    HIDE_ADD_BUILD_BUTTON => 1,
  });

  my ($menu_res) = $Api->api_call({
    PATH   => '/portal/menus',
    PARAMS => { URL => '_EMPTY_' }
  });

  my $menus_without_url = $menu_res->{list} || [];

  $web_params{PORTAL_MENU_ID} = $html->form_select('portal_menu_id', {
    SELECTED    => $FORM{PORTAL_MENU_ID},
    SEL_LIST    => $menus_without_url,
    SEL_OPTIONS => { '' => $lang{CHOOSE_MENU} },
    NO_ID       => 1,
    REQUIRED    => 1
  });

  if (!$FORM{STATUS} or $FORM{STATUS} == 0) {
    $web_params{SHOWED} = '';
    $web_params{HIDDEN} = 'checked=\'checked\'';
  }
  else {
    $web_params{SHOWED} = 'checked=\'checked\'';
    $web_params{HIDDEN} = '';
  }

  if ($FORM{ON_MAIN_PAGE}) {
    $web_params{ON_MAIN_PAGE_CHECKED} = 'checked=\'checked\'';
  }

  my $all_methods = \%Abills::Sender::Core::PLUGIN_NAME_FOR_TYPE_ID;
  my $send_methods = $Sender->available_types(
    { HASH_RETURN => 1, SOFT_CHECK => 1 }
  );

  foreach my $allowed_method (@{+ALLOWED_METHODS}) {
    my ($method_id) = grep { $_ eq $allowed_method } keys %$send_methods;
    my $key = uc($all_methods->{$allowed_method});
    if (!defined($method_id)) {
      $web_params{$key.'_NOT_EXIST'} = 'hidden';
    }
    else {
      $web_params{$key . '_METHOD_ID'} = $method_id;
      $web_params{$key . '_START_DATETIME'} = $html->form_datetimepicker($key . '_START_DATETIME',
        undef,
        { FORMAT  => 'YYYY-MM-DD HH:mm:ss' }
      );
      if ($FORM{chg} || $FORM{change}) {
        $web_params{$key . '_BUTTON_SEND'} = _portal_send_again_button($method_id);
      }
    }
  }

  $web_params{BUTTON_ADD_ATTACHMENTS} = _portal_upload_button({ LIST => 1 });

  my $change_article_info = {};

  if ($FORM{add}) {
    my ($res) = $Api->api_call({
      METHOD => 'POST',
      PATH   => '/portal/articles',
      PARAMS => { %FORM }
    });

    if (!$res->{errno}) {
      my @add_letters = ();
      for my $key (keys %FORM) {
        if ($key =~ /^NEWSLETTER_(.+)/g) {
          my $type = $1;
          push @add_letters, $type;
        }
      }

      if (@add_letters) {
        my $last_news_id = $res->{INSERT_ID};
        for my $method (@add_letters) {
          my ($method_id) = grep { uc($send_methods->{$_}) eq $method } keys %$send_methods;
          my ($newsletter_res) = $Api->api_call({
            METHOD => 'POST',
            PATH   => '/portal/newsletter',
            PARAMS => {
              PORTAL_ARTICLE_ID => $last_news_id,
              SEND_METHOD       => $method_id,
              START_DATETIME     => $FORM{$method . '_START_DATETIME'}
            }
          });
          if ($newsletter_res->{errno}) {
            $res->{errno} = $newsletter_res->{errno};
            $res->{errstr} = $newsletter_res->{errstr};
          }
        }
      }
    }

    if (!_error_show($res)) {
      $html->message('info', $lang{ADDED});
    }
  }
  elsif ($FORM{del}) {
    my ($res) = $Api->api_call({
      METHOD => 'DELETE',
      PATH   => "/portal/articles/$FORM{del}",
    });

    if (!_error_show($res)) {
      $html->message('info', $lang{INFO}, "$lang{DELETED}");
    }

  }
  elsif ($FORM{change} && $FORM{id}) {
    my %body_props = ();
    # TODO: make this from frontend
    if ($FORM{RESET} && $FORM{RESET} == 1){
      $FORM{DISTRICT_ID} = 0;
      $FORM{STREET_ID} = 0;
      $FORM{BUILD_ID} = 0;
      $FORM{ADDRESS_FLAT} = '';
    }

    if (!$FORM{deeplink}) {
      $body_props{deeplink} = 0;
    }

    my ($res) = $Api->api_call({
      METHOD => 'PUT',
      PATH   => "/portal/articles/$FORM{id}",
      PARAMS => { %FORM, %body_props }
    });

    if (!_error_show($res)) {
      $html->message('info', $lang{CHANGED});
    }
  }
  elsif ($FORM{chg}) {
    $web_params{ACTION}     = 'change';
    $web_params{ACTION_LNG} = $lang{CHANGE};
    $web_params{TITLE_NAME} = $lang{CHANGE_MENU};

    my ($article_info) = $Api->api_call({
      PATH   => "/portal/articles/$FORM{chg}",
    });

    $change_article_info = $article_info;
    my ($newsletters_res) = $Api->api_call({
      PATH   => "/portal/newsletter",
      PARAMS => { ID => $FORM{chg} }
    });

    my $to_changed_newsletters = $newsletters_res->{list} || [];
    for my $selected (@$to_changed_newsletters) {
      my $method_id = $selected->{send_method};
      if (defined($send_methods->{$method_id})) {
        my $defined_method = $send_methods->{$method_id};
        my $key = uc($defined_method);
        $web_params{$key . '_SELECTED'} = 'checked';
        $web_params{$key . '_METHOD_ID'} = $method_id;
        my $is_disabled = 0;
        if ($selected->{status} == 0) {
          $is_disabled = 1;
          $web_params{$key . '_BUTTON_SEND'} = $lang{NEWSLETTER_ALREADY_EXIST};
        }

        $web_params{$key . '_START_DATETIME'} = $html->form_datetimepicker(
          $key . '_START_DATETIME',
          $selected->{start_datetime},
          { FORMAT  => 'YYYY-MM-DD HH:mm:ss', DISABLED => $is_disabled }
        );
      }
    }
    $web_params{PORTAL_MENU_ID} = $html->form_select('portal_menu_id', {
      SELECTED    => $article_info->{portal_menu_id},
      SEL_LIST    => $menus_without_url,
      NO_ID       => 1,
      SEL_OPTIONS => { '' => $lang{CHOOSE_MENU} }
    });

    if (!$change_article_info->{status}) {
      $web_params{SHOWED} = '';
      $web_params{HIDDEN} = 'checked=\'checked\'';
    }
    else {
      $web_params{SHOWED} = 'checked=\'checked\'';
      $web_params{HIDDEN} = '';
    }

    if ($change_article_info->{picture}) {
      my $picture_preview_icon = $html->element('i', '', { class => 'fas fa-image' });
      $web_params{PICTURE_PREVIEW_LINK} = $html->element('a',
        $picture_preview_icon,
        {
          class => 'btn input-group-button',
          href => $change_article_info->{picture}
        }, { OUTPUT2RETURN => 1 });
    }

    if ($change_article_info->{on_main_page}) {
      $web_params{ON_MAIN_PAGE_CHECKED} = 'checked=\'checked\'';
    }

    if(!$change_article_info->{archive}) {
      $web_params{SHOWED_ARCHIVE} = '';
      $web_params{HIDDEN_ARCHIVE} = 'checked=\'checked\'';
    }
    else {
      $web_params{SHOWED_ARCHIVE} = 'checked=\'checked\'';
      $web_params{HIDDEN_ARCHIVE} = '';
    }

    if ($change_article_info->{deeplink}) {
      $web_params{DEEPLINK} = 'checked';
    }

    $web_params{IMPORTANCE_STATUS} =  $html->form_select('importance', {
      SELECTED       => $change_article_info->{importance},
      SEL_ARRAY      => \@IMPORTANCE_STATUS,
      ARRAY_NUM_ID   => 'id',
      SEL_VALUE      => 'name',
      NO_ID          => 1
    });

    $web_params{TAGS} = $html->form_select('TAGS', {
      SELECTED       => $change_article_info->{tags},
      SEL_LIST       => $tags_list,
      SEL_KEY        => 'id',
      SEL_VALUE      => 'name',
      NO_ID          => 1,
      SEL_OPTIONS    => {"" => ""}
    });

    $web_params{DOMAIN_ID} = $html->form_select('DOMAIN_ID', {
      SELECTED       => $change_article_info->{domain_id},
      SEL_LIST       => $domains_list,
      SEL_KEY        => 'id',
      SEL_VALUE      => 'name',
      NO_ID          => 1,
      SEL_OPTIONS    => {"" => ""}
    });

    $web_params{GROUPS} = sel_groups({
      FILTER_SEL => 1,
      GID        => $change_article_info->{gid}
    });

    $web_params{ADDRESS_FORM} = form_address_select({
      DISTRICT_ID  => $change_article_info->{district_id},
      STREET_ID    => $change_article_info->{street_id},
      BUILD_ID     => $change_article_info->{build_id},
      ADDRESS_FLAT => $change_article_info->{address_flat},
      HIDE_ADD_BUILD_BUTTON => 1,
    })
  }

  if (!$FORM{add} and !$FORM{change}) {
    $web_params{CURRENTLY_ADDED} = 'disabled' if $FORM{chg};
    $html->tpl_show(_include('portal_article_add', 'Portal'), {
      %web_params,
      %$change_article_info,
    });
  }

  my $table = $html->table({
      width      => '100%',
      caption    => $lang{ARTICLES},
      title      => [
        'ID',
        $lang{MENU},
        $lang{PICTURE},
        $lang{TITLE},
        $lang{DATE_PUBLICATE},
        $lang{DATE_END_PUBLICATE},
        $lang{STATUS},
        $lang{ON_MAIN_PAGE},
        $lang{USER_PORTAL},
        $lang{IMPORTANCE},
        $lang{GROUPS},
        $lang{TAGS},
        $lang{DOMAINS},
        $lang{DISTRICTS},
        $lang{ADDRESS_STREET},
        '-',
        '-'
      ],
      DATA_TABLE => {
        order => [ [0, 'desc'] ],
        columnDefs =>  [ { type => 'num' }],
        lengthMenu => [ [ 50, 100, -1 ], [ 50, 100, $lang{ALL} ] ]
      },
      cols_align => [ 'left', 'left', 'left', 'left', 'right','right', 'center', 'center', 'center' ],
      ID         => 'PORTAL_ARTICLES',
    }
  );

  my @STATUS     = ("<span>$lang{HIDDEN}</span>",
                "<span class='text-success text-bold'>$lang{SHOWED}</span>");
  my @PICTURE_STATUS  = ("<span>$lang{NO}</span>",
                "<span class='text-success text-bold'>$lang{YES}</span>");
  my @ARCHIVE    = ("<span class='text-success text-bold'>$lang{SHOW}</span>",
                "<span>$lang{TO_ARCHIVE}</span>");
  my @IMPORTANCE = ("<span>$lang{NORMAL}</span>",
                "<span class='text-danger text-bold'>$lang{CRITICAL}</span>");


  my ($res) = $Api->api_call({
    PATH   => '/portal/articles',
    PARAMS => { %LIST_PARAMS, PAGE_ROWS => 10000 }
  });

  my $list = $res->{list} || [];

  my $groups_info = $users->groups_list({
    GID             => '_SHOW',
    NAME            => '_SHOW',
    DESCR           => '_SHOW',
    ALLOW_CREDIT    => '_SHOW',
    DISABLE_PAYSYS  => '_SHOW',
    DISABLE_CHG_TP  => '_SHOW',
    USERS_COUNT     => '_SHOW',
    COLS_NAME       => 1,
  });

  foreach my $line (@$list) {
    my $tag_name;
    if (in_array('Tags', \@MODULES)) {
      my $tag_info = $Tags->info($line->{tags});
      if ($Tags->{TOTAL}) {
        $tag_name   = $tag_info->{NAME};
      }
    }

    my $domain_name;
    if (in_array('Multidoms', \@MODULES)) {
      my $domain_info = $Multidoms->multidoms_domain_info({
        ID => $line->{domain_id}
      });

      if ($Multidoms->{TOTAL}) {
        $domain_name   = $domain_info->{NAME};
      }
    }

    my $group_name = '';

    # Attention: gid 0 is actually "without group" but
    # I make visible for all groups - otherwise it will break legacy
    if ($line->{gid} eq '0' || $line->{gid} eq '*' || $line->{gid} eq '') {
      $group_name = $lang{ALL};
    } else {
      my @ids = split(/,\s+/, $line->{gid});
      for my $id (@ids) {
        my ($matched_group) = grep { "$_->{gid}" eq $id } @$groups_info;
        if ($matched_group) {
          my $delimiter_or_not = length($group_name) != 0 ? ', ' : '';
          $group_name .=  $delimiter_or_not . $matched_group->{name};
        }
      }
    }

    $table->addrow(
      $line->{id},
      $line->{name},
      $line->{picture} ? $PICTURE_STATUS[1] : $PICTURE_STATUS[0],
      $line->{title},
      $line->{date},
      $line->{end_date},
      (defined($line->{status}) && $STATUS[ $line->{status} ]) || $line->{status},
      (defined($line->{on_main_page}) && $line->{on_main_page} == 1) ? $lang{YES} : $lang{NO},
      (defined($line->{archive}) && $ARCHIVE[ $line->{archive} ]) || $line->{archive},
      (defined($line->{importance}) && $IMPORTANCE[ $line->{importance} ]) || $line->{importance},
      $group_name,
      $tag_name,
      $domain_name,
      $line->{dis_name},
      $line->{st_name},
      $html->button($lang{INFO}, "index=$index&chg=$line->{id}", { class => 'change' }),
      (defined($permissions{0}{5})) ? $html->button($lang{DEL}, "index=$index&del=$line->{id}", { MESSAGE => "$lang{DEL} ". ($line->{title}|| q{}) . "?", class => 'del' }) : ''
    );
  }

  print $table->show();

  return 1;
}

#***********************************************************
=head2 portal_user_cabinet()  User cabinet news

=cut
#***********************************************************
sub portal_user_cabinet {
  my ($res) = $Api->api_call({
    PATH => '/user/portal/news'
  });

  my $list = $res->{news} || [];
  my @reversed = reverse @$list;

  my %USER_NEWS;
  my @NORMAL;
  my @IMPORTANT;

  my %templates = (
    portal_user_content_picture => _include('portal_user_content_picture', 'Portal'),
    portal_user_content         => _include('portal_user_content', 'Portal')
  );

  foreach my $i (0..$#reversed) {
    my $item = $reversed[$i];
    my $active = ($#reversed == $i) ? 'active' : '';
    my $important = $item->{important} ? 'important' : '';
    my $template = $item->{picture} ? 'portal_user_content_picture' : 'portal_user_content';
    my $content = $html->tpl_show($templates{$template}, {
      TITLE             => $item->{title},
      SHORT_DESCRIPTION => $item->{short_description},
      ACTIVE            => $active,
      HREF              => "$SELF_URL?article=".$item->{id},
      PICTURE_SRC       => $item->{picture},
      IMPORTANT         => $important,
    },
    {
      OUTPUT2RETURN => 1,
      ID            => 'ARTICLE_'. $item->{id}
    });

    if ($important) {
      unshift(@IMPORTANT, $content);
    } else {
      unshift(@NORMAL, $content)
    }
  }

  $USER_NEWS{CONTENT} .= join('', @IMPORTANT, @NORMAL);

  for my $i (0..$#reversed) {
    if ($i == 0) {
      $USER_NEWS{INDICATORS} .= "<li data-target='#myPortalCarousel' data-slide-to='$i' class='active'></li>";
    }
    else{
      $USER_NEWS{INDICATORS} .= "<li data-target='#myPortalCarousel' data-slide-to='$i' ></li>";
    }
  }

  my $news_body = '';
  if ($USER_NEWS{CONTENT}){
    $news_body = $html->tpl_show(_include('portal_user_news', 'Portal'), {
      %USER_NEWS
    }, { OUTPUT2RETURN     => 1 });
  }

  return $news_body;
}

#**********************************************************
=head2 portal_start_page() -

  Arguments:
    $attr -
  Returns:

  Examples:

=cut
#**********************************************************
sub portal_s_page {
  my $lang = $FORM{language} || $html->{language} || 'english';

  do "../Abills/modules/Portal/lng_$lang.pl";
  do "../language/$lang.pl";

  my %LANG;

  if ($conf{LANGS}) {
    $conf{LANGS} =~ s/\n//g;

    my (@lang_arr) = split(/;/, $conf{LANGS});
    %LANG = ();

    foreach my $l (@lang_arr) {
      my $lang_name = q{};
      ($lang, $lang_name) = split(/:/, $l);
      $lang =~ s/^\s+//;
      $LANG{$lang} = $lang_name;
    }
  }

  our %QT_LANG;
  our %OUTPUT;
  $OUTPUT{HTML_STYLE} = 'default';
  $OUTPUT{SEL_LANGUAGE} = $html->form_select('language', {
    EX_PARAMS  => 'onChange="selectLanguage()"',
    SELECTED   => $html->{language},
    SEL_HASH   => \%LANG,
    NO_ID      => 1,
    EXT_PARAMS => { qt_locale => \%QT_LANG }
  });

  my $url = '';

  my ($res) = $Api->api_call({
    PATH => '/user/portal/news/'
  });

  my $list = $res->{topics} || [];

  my @portal_menu_template = (
    _include('portal_menu_hovered', 'Portal'),
    _include('portal_menu', 'Portal')
  );

  my @MENUS = ();

  foreach my $line (@$list) {
    $url = !$line->{url} ? "$SELF_URL?menu_category=" . $line->{id} : $line->{url};

    my $is_default = (!$FORM{menu_category} || $FORM{menu_category} != $line->{id});
    push @MENUS, $html->tpl_show($portal_menu_template[$is_default], {
      HREF      => $url,
      MENU_NAME => $line->{name},
    }, { OUTPUT2RETURN => 1 });
  }

  $OUTPUT{MENU} = scalar @MENUS ? join('', @MENUS) : $lang{NO_MENU};

  my @content_templates = (
    _include('portal_content_picture', 'Portal'),
    _include('portal_content', 'Portal'),
  );

  my @CONTENTS = ();
  if ($FORM{menu_category}) {
    my ($articles_res) = $Api->api_call({
      PATH   => '/user/portal/news',
      PARAMS => { PORTAL_MENU_ID => $FORM{menu_category} }
    });

    $list = $articles_res->{news} || [];

    foreach my $line (@$list) {
      push @CONTENTS, $html->tpl_show($content_templates[!$line->{picture}], {
        HREF              => "$SELF_URL?article=" . $line->{id},
        TITLE             => $line->{title},
        DATE              => $line->{date},
        SHORT_DESCRIPTION => $line->{short_description},
        PICTURE_SRC       => $line->{picture}
      }, { OUTPUT2RETURN => 1 });
    }

  }
  elsif ($FORM{article}) {
    my ($article_res) = $Api->api_call({
      PATH   => "/user/portal/news/$FORM{article}/",
    });

    my $full_article = $article_res->{news}->[0];

    if ($full_article) {
      my $text = $full_article->{content};
      my $picture = $full_article->{picture};

      my $text_article = is_html($text)
        ? $text
        : convert($text, { text2html => 1 });

      my $template = $picture ? 'portal_article_picture' : 'portal_article';
      push @CONTENTS, $html->tpl_show(_include($template, 'Portal'), {
        TITLE             => $full_article->{title},
        ARTICLE           => $text_article || $full_article->{short_description},
        DATE              => $full_article->{date},
        PICTURE_SRC       => $picture
      }, { OUTPUT2RETURN => 1 });
    }
  }
  else {
    my ($articles_res) = $Api->api_call({
      PATH   => '/user/portal/news',
      PARAMS => { MAIN_PAGE => 1 }
    });

    $list = $articles_res->{news} || [];

    foreach my $line (@$list) {
      push @CONTENTS, $html->tpl_show($content_templates[!$line->{picture}], {
        HREF              => "$SELF_URL?article=" . $line->{id},
        TITLE             => $line->{title},
        DATE              => $line->{date},
        SHORT_DESCRIPTION => $line->{short_description},
        PICTURE_SRC       => $line->{picture}
      }, { OUTPUT2RETURN => 1 });
    }
  }

  if (!scalar @CONTENTS) {
    push @CONTENTS, $html->tpl_show(_include('portal_article', 'Portal'), {
      TITLE   => '',
      ARTICLE => $lang{NO_DATA}
    }, { OUTPUT2RETURN => 1 });
  };

  if (@REGISTRATION) {
    my $menu_registration_template = _include('portal_menu_registration', 'Portal');
    $OUTPUT{REGISTRATION} = $html->tpl_show($menu_registration_template,
      {
        HREF      => "registration.cgi?module=$REGISTRATION[0]"
      },
      { OUTPUT2RETURN => 1 }
    );
    $OUTPUT{REGISTRATION_MOBILE} = $html->tpl_show($menu_registration_template,
      {
        CLASS     => 'd-lg-none',
        HREF      => "registration.cgi?module=$REGISTRATION[0]",
      },
      { OUTPUT2RETURN => 1 }
    );
  }

  $OUTPUT{SOCIAL_AUTH_BLOCK} = make_social_auth_login_buttons();
  $OUTPUT{SOCIAL_LINKS_HEADER} = _portal_social_links();

  $OUTPUT{CONTENT} = join('', @CONTENTS);

  if ($conf{COOKIE_POLICY_VISIBLE} && $conf{COOKIE_URL_DOC}) {
    $OUTPUT{COOKIE_POLICY_VISIBLE} = 'block';
    $OUTPUT{COOKIE_URL_DOC} = $conf{COOKIE_URL_DOC};
  }
  else {
    $OUTPUT{COOKIE_POLICY_VISIBLE} = 'none';
  }

  if ($conf{user_background}) {
    $OUTPUT{BACKGROUND_COLOR} = $conf{user_background};
  }
  elsif ($conf{user_background_url}) {
    $OUTPUT{BACKGROUND_URL} = $conf{user_background_url};
  }

  print $html->tpl_show(_include('portal_body', 'Portal'), { %OUTPUT });

  return 1;
}

#***********************************************************
=head2 portal_newsletters() -  Portal newsletters info

=cut
#***********************************************************
sub portal_newsletters {
  my ($result) = $Api->api_call({
    PATH         => "/portal/newsletter",
    PARAMS       => { %FORM, %LIST_PARAMS }
  });
  my $letters = $result->{list} || [];

  require Abills::Sender::Core;
  my $send_methods = \%Abills::Sender::Core::PLUGIN_NAME_FOR_TYPE_ID;

  my $table = $html->table({
    width   => '100%',
    caption => $lang{ARTICLES},
    title   => [
      "ID",
      $lang{TITLE},
      $lang{METHOD},
      $lang{STATUS},
      $lang{SENT},
      $lang{DATE}
    ],
    qs      => $pages_qs,
    pages   => $result->{total},
    ID      => 'PORTAL_TYPES',
  });

  my @STATUSES = (
    $lang{ENABLED},
    $lang{SENT},
    $lang{ERROR},
    $lang{PROCESSING}
  );

  for my $letter (@$letters) {
    $table->addrow(
      $letter->{id},
      $letter->{title},
      $send_methods->{$letter->{send_method}},
      $STATUSES[$letter->{status}] || '',
      $letter->{sent},
      $letter->{start_datetime},
    )
  }

  print $table->show();

  return 1;
}

#***********************************************************
=head2 portal_attachments($attr) - list of attachments

=cut
#***********************************************************
sub portal_attachments {
  if ($FORM{del}) {
    $Api->api_call({
      METHOD => 'DELETE',
      PATH   => "/portal/attachment/$FORM{del}/",
    });
  }

  my $add_button = _portal_upload_button({ LIST => 0 });

  my ($result) = $Api->api_call({
    PATH   => '/portal/attachment',
    PARAMS => { %FORM, %LIST_PARAMS }
  });

  result_former({
    DATAHASH        => $result->{list},
    DATATOTAL       => $result->{total},
    DEFAULT_FIELDS  => 'id,filename,file_type,file_size,uploaded_at,src',
    SKIP_USER_TITLE => 1,
    FUNCTION_FIELDS => 'del',
    EXT_TITLES      => {
      id          => 'ID',
      filename    => $lang{NAME},
      file_type   => $lang{TYPE},
      file_size   => $lang{SIZE},
      uploaded_at => $lang{ADDED},
      src         => $lang{LINK}
    },
    TABLE           => {
      width       => '100%',
      caption     => $lang{PORTAL_ATTACHMENTS},
      qs          => $pages_qs,
      ID          => 'PORTAL_ATTACHMENTS',
      EXPORT      => 1,
      MENU        => [ $add_button ],
      SHOW_COLS   => 0,
    },
    MAKE_ROWS       => 1,
    TOTAL           => 1,
  });
}

#***********************************************************
=head2 portal_attachments_ajax_modal($attr) - ajax attachment table

=cut
#***********************************************************
sub portal_attachments_ajax_modal {
  my $table_list = '';
  if ($FORM{SHOW_LIST}) {
    my ($result) = $Api->api_call({
      PATH   => '/portal/attachment/',
      PARAMS => {
        PAGE_ROWS   => 10000,
        DESC        => 'DESC'
      }
    });
    my $attachments_list = $result->{list} || [];
    my $template = _include('portal_attachment_card_preview', 'Portal');

    $table_list = join '', map {
      $html->tpl_show($template, {
        ATTACH_ID   => $_->{id},
        FILENAME    => $_->{filename},
        UPLOADED_AT => $_->{uploaded_at},
        LINK        => $_->{src},
      }, { OUTPUT2RETURN => 1 })
    } @$attachments_list;
  }

  $table_list = $html->element('div', $table_list, { class => 'row' }, { OUTPUT2RETURN => 1 });
  $html->tpl_show(_include('portal_attachments_list_add', 'Portal'), { ATTACHMENT_LIST => $table_list });
}

#**********************************************************
=head2 _portal_social_links($title) - creates links for header

  Arguments:
    None

  Returns:
    $all_links

=cut
#**********************************************************
sub _portal_social_links {
  my %bots = ();

  $bots{telegram} = $conf{TELEGRAM_BOT_NAME} if ($conf{TELEGRAM_BOT_NAME});
  $bots{viber} = $conf{VIBER_BOT_NAME} if ($conf{VIBER_BOT_NAME});

  my %bot_links = (
    telegram => 'https://t.me/',
    viber    => 'viber://pa?chatURI='
  );

  my $social_links = $conf{SOCIAL_NETWORKS};

  if ($social_links || scalar %bots) {
    my $all_links = '';
    my $template = _include('portal_social_link', 'Portal');

    if (!(defined($social_links) && $social_links)) {
      for my $key_bot (sort keys %bots) {
        if (!$bots{$key_bot}) {
          next;
        }

        my $bot_name = $bots{$key_bot};
        my $full_link = $bot_links{$key_bot} . $bot_name;

        my $button = $html->tpl_show($template, {
          HREF   => $full_link,
          SOCIAL => $key_bot
        }, { OUTPUT2RETURN => 1 });

        $all_links .= $button;
      }
    } else {
      for my $key_bot (sort keys %bots) {
        if ($bots{$key_bot}) {
          $social_links->{$key_bot} = $bots{$key_bot};
        }
      }
      for my $social (sort keys %$social_links) {
        if (defined($bots{$social}) && $bots{$social}) {
          my $bot_name = $bots{$social};
          my $full_link = $bot_links{$social} . $bot_name;

          my $button = $html->tpl_show($template, {
            HREF   => $full_link,
            SOCIAL => $social
          }, { OUTPUT2RETURN => 1 });
          $all_links .= $button;
        } elsif (defined($social_links->{$social})) {
          my $button = $html->tpl_show($template, {
            HREF   => $social_links->{$social},
            SOCIAL => $social
          }, { OUTPUT2RETURN => 1 });
          $all_links .= $button;
        }
      }
    }

    return $all_links;
  }

  return '';
}

#**********************************************************
=head2 _portal_upload_button($attr)

  Arguments:
    $attr - {
      LIST => boolean - show list or not
    }

  Returns:
    $button - string

=cut
#**********************************************************
sub _portal_upload_button {
  my ($attr) = @_;
  my $add_icon = $html->element('i', '', { class => 'fas fa-paperclip mr-1' });

  $html->button($add_icon . $lang{PORTAL_ATTACHMENTS}, '', {
    class     => 'btn btn-xs btn-primary',
    SKIP_HREF => 1,
    ex_params => qq{onClick='loadToModal("?get_index=portal_attachments_ajax_modal&header=2&AJAX=1&SHOW_LIST=$attr->{LIST}", "", "xl")' style='min-width: max-content'}
  })
}

#**********************************************************
=head2 _portal_send_again_button($method_id)

  Arguments:
    $method_id - int

  Returns:
    $button - newsletter again button

=cut
#**********************************************************
sub _portal_send_again_button {
  my ($method_id) = @_;

  return $html->element(
    'button',
    $lang{NEWSLETTER_AGAIN},
    {
      class                   => 'btn btn-xs btn-success',
      type                    => 'button',
      'data-method-id'        => $method_id,
      'data-label-on-loading' => "$lang{LOADING}...",
      'data-label-on-error'   => $lang{ERROR},
      'data-label-on-success' => $lang{SUCCESS},
    }
  );
}

1;
