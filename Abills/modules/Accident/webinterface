=head1 Accident

  Accident 

  Error ids: 50xx

=cut

use strict;
use warnings FATAL => 'all';
use Time::Local qw(timelocal);

use Control::Services;
use Accident;
use Address;
use Users;
use Abills::Sender::Core;
use Abills::Base qw \in_array date_inc\;
require Abills::Misc;

our (
  $db,
  $admin,
  %conf,
  %lang,
  @MONTHES,
  %permissions
);

our Abills::HTML $html;

if (
  form_purchase_module(
    {
      HEADER          => 1,
      MODULE          => 'Accident',
      REQUIRE_VERSION => 8.09,
    }
  )
) {
  exit;
}

my %priority = (
  0 => $lang{VERY_LOW},
  1 => $lang{LOW},
  2 => $lang{NORMAL},
  3 => $lang{HIGH},
  4 => $lang{VERY_HIGH}
);

my %status = (
  0 => $lang{PROCESSING},
  1 => $lang{PROCESSED},
  2 => $lang{CLOSED},
);

my @STATUSES_COLORS = ('text-primary', 'text-warning', '#2D9F31');

my $Accident = Accident->new($db, $admin, \%conf);
my $Address = Address->new($db, $admin, \%conf);
my $Users = Users->new($db, $admin, \%conf);
my $Sender = Abills::Sender::Core->new($db, $admin, \%conf);
my $Equipment;

if ($conf{EQUIPMENT_LOG} && in_array('Equipment', \@MODULES)) {
  require Equipment;
  Equipment->import();
  $Equipment = Equipment->new($db, $admin, \%conf);
}

#**********************************************************
=head2 accident_log()

  Arguments:
     -

  Returns:
    -

=cut
#**********************************************************
sub accident_log {
  my $index = get_function_index('accident_log');

  if ($FORM{del} && $FORM{COMMENTS}) {
    $Accident->address_del({
      ID => $FORM{ID}
    });

    $Accident->del({
      ID => $FORM{ID}
    });

    if (!_error_show($Accident)) {
      $html->message('info', $lang{SUCCESS}, $lang{OPERATION});
    }
  }

  if ($FORM{SAVE}) {
    my @id_success = split(/, /, $FORM{ID} ? $FORM{ID} : '');

    foreach my $chg_element (@id_success) {
      $Accident->change_element({
        ID     => $chg_element,
        STATUS => 2
      });
    }
    if (!_error_show($Accident)) {
      $html->message("info", $lang{SUCCESS}, $lang{OPERATION});
    }
  }

  if ($FORM{add_form}) {
    accident_log_add();
  }
  else {
    my $select_admin = sel_admins();

    my $select_priority = $html->form_select(
      'PRIORITY',
      {
        SELECTED    => $FORM{PRIORITY},
        SORT_VALUE  => 1,
        SORT_KEY    => 1,
        NO_ID       => 1,
        SEL_HASH    => \%priority,
        SEL_OPTIONS => { '' => '' }
      },
      { class => 'form-control' }
    );

    my $datepicker = $html->form_daterangepicker({
      NAME      => 'FROM_DATE/TO_DATE',
      FORM_NAME => 'accident_log',
      VALUE     => $FORM{'FROM_DATE_TO_DATE'},
      RETURN_INPUT => 1
    });
    my $select_status = $html->form_select(
      'STATUS',
      {
        SELECTED       => $FORM{STATUS},
        SEL_HASH     => \%status,
        NO_ID        => 1,
        SORT_KEY_NUM => 1,
        SEL_OPTIONS  => { '' => '' }
      },
      { class => 'form-control' }
    );

    my $form_city = form_address_select2({
      %FORM,
      HIDE_FLAT             => 1,
      HIDE_ADD_BUILD_BUTTON => 1,
    }, { class => 'form-control' });

    form_search({
      TPL => $html->tpl_show(_include('accident_log_search', 'Accident'), {
        INDEX           => $index,
        SELECT_PRIORITY => $select_priority,
        DATE_PCIKER     => $datepicker,
        SELECT_ADDRESS  => $form_city,
        SELECT_ADMIN    => $select_admin,
        SELECT_STATUS   => $select_status,
      }, { OUTPUT2RETURN => 1 })
    });
  }

  my Abills::HTML $table;
  my $acc_list;

  my %EXT_TITLES = (
    'id'         => '#',
    'priority'   => $lang{PRIORITY},
    'name'       => $lang{NAME},
    'date'       => $lang{DATE},
    'status'     => $lang{STATUS},
    'descr'      => $lang{DESC},
    'aid'        => $lang{ADMIN},
    'end_time'   => $lang{WORK_END_DATE},
    'realy_time' => $lang{WORK_REALY_DATE},
    'address_id' => $lang{ADDRESS},
  );

   ($table, $acc_list) = result_former({
    INPUT_DATA      => $Accident,
    FUNCTION        => 'list',
    DEFAULT_FIELDS  => 'ID,PRIORITY,NAME,DATE,STATUS,DESCR,AID,END_TIME,REALY_TIME',
    HIDDEN_FIELDS   => 'TYPE_ID,BUILD_ID,STREET_ID,DISTRICT_ID',
    SKIP_USER_TITLE => 1,
    EXT_TITLES      => \%EXT_TITLES,
    TABLE           => {
      width   => '100%',
      caption => $lang{ACCIDENT_LOG},
      qs      => $pages_qs,
      title   => [ $lang{PRIORITY} ],
      ID      => 'ACCIDENT_LOG',
      #MENU    => "$lang{ADD}:form_add=1&index=" . get_function_index('accident_log_add') . ":add;",
      MENU    => "$lang{ADD}:add_form=1&index=$index:add;",
    },
  });

  my $dublicat = 0;
  foreach my $line (@$acc_list) {
    my @fields_array = ();

    next if ($line->{id} && $dublicat && $dublicat == $line->{id});

    $dublicat = $line->{id};
    for (my $i = 0; $i < $Accident->{SEARCH_FIELDS_COUNT}; $i++) {
      my $field_name = $Accident->{COL_NAMES_ARR}->[$i] || '';

      if ($field_name eq "priority") {
        $line->{priority} = $priority{ $line->{priority} };
      }
      elsif ($field_name eq "status") {
        $line->{status} = $html->color_mark($status{ $line->{status} }, $STATUSES_COLORS[ $line->{status} ]);
      }
      elsif ($field_name eq "aid") {
        my $admins_list = sel_admins({ HASH => 1 });
        $line->{aid} = $admins_list->{ $line->{aid} };
      }
      elsif ($field_name eq "address_id") {
        $line->{address_id} = $line->{TYPE_IDS};
      }
      elsif ($field_name eq "type_id") {
        $line->{type_id} = '';
      }

      push @fields_array, $line->{$field_name};
    }

    push @fields_array, $html->button($lang{CHANGE}, "&change=$line->{id}&index=" . get_function_index('accident_log_add'),
      { ICON => 'fa fa-pencil' });
    push @fields_array, $html->button($lang{DEL}, "&del=1&ID=" . ($line->{id} || '') . "&index=" . get_function_index('accident_log'),
      { MESSAGE => $lang{DEL}, class => 'text-danger', ICON => 'fa fa-trash' });
    push @fields_array, $html->button('', "&compensation=1&ID=" . ($line->{id} || '') . "&index=" . get_function_index('accident_compensation'),
      { ICON => 'fa fa-check' });

    $table->addrow(@fields_array);
  }
  print $table->show();

  return 0;
}

#**********************************************************
=head2 accident_log_add()

  Arguments:
     -

  Returns:
    -
=cut
#**********************************************************
sub accident_log_add {
  #my $admin_select = sel_admins({ HASH => 1 });

  my @address_array = ();
  my @district_array = ();
  my @street_array = ();
  my @city_array = ();
  my $address_list;
  my $type_id = 0;

  my $builds_list = $Address->build_list({
    ID            => '_SHOW',
    STREET_ID     => '_SHOW',
    STREET_NAME   => '_SHOW',
    DISTRICT_NAME => '_SHOW',
    DISTRICT_ID   => '_SHOW',
    NUMBER        => '_SHOW',
    CITY          => '_SHOW',
    LOCATION_ID   => '_SHOW',
    COLS_NAME     => 1,
    PAGE_ROWS     => 100000,
  });

  my $info;

  if ($FORM{change}) {
    $address_list = $Accident->accident_address_info($FORM{change});
    $info = $Accident->accident_info($FORM{change});

  }
  elsif ($FORM{chg}) {
    if ($FORM{USER_ACCIDENT}) {
      $Accident->change_element({
        ID => $FORM{al_id},
        %FORM
      });
    }
    else {
      $Accident->address_del({
        ID => $FORM{al_id}
      });

      $Accident->del({
        ID => $FORM{al_id}
      });
    }
  }

  if ($FORM{add} || $FORM{chg}) {
    my $accident_id;
    unless ($FORM{USER_ACCIDENT}) {
      $Accident->add({ %FORM });

      $accident_id = $Accident->{INSERT_ID};

      if ($FORM{GEOLOCATION}) {
        $type_id = 1;
        @district_array = split(/, /, $FORM{GEOLOCATION} ? $FORM{GEOLOCATION} : '');

        accident_element_add({
          ADDRESS   => \@district_array,
          TYPE_ID   => $type_id,
          INSERT_ID => $accident_id,
        });
      }

      if ($FORM{CITY}) {
        $type_id = 2;

        @city_array = split(/, /, $FORM{CITY} ? $FORM{CITY} : '');

        accident_element_add({
          ADDRESS   => \@city_array,
          TYPE_ID   => $type_id,
          INSERT_ID => $accident_id,
        });
      }

      if ($FORM{STREET}) {
        $type_id = 3;
        @street_array = split(/, /, $FORM{STREET} ? $FORM{STREET} : '');

        accident_element_add({
          ADDRESS   => \@street_array,
          TYPE_ID   => $type_id,
          INSERT_ID => $accident_id,
        });
      }

      if ($FORM{BUILD}) {
        $type_id = 4;
        @address_array = split(/, /, $FORM{BUILD} ? $FORM{BUILD} : '');

        accident_element_add({
          ADDRESS   => \@address_array,
          TYPE_ID   => $type_id,
          INSERT_ID => $accident_id,
        });
      }
      else {
        $html->message('err', $lang{ERROR}, "$lang{ADDRESS} $lang{EMPTY_FIELD}", { ID => 5012 });
        return 1;
      }
    }

    if (!_error_show($Accident)) {
      $html->message('info', $lang{SUCCESS}, $lang{OPERATION}.' #'.$accident_id );

      if ($accident_id && $conf{ACCIDENT_WARNING} && $FORM{add}) {
        $address_list = $Accident->accident_address_info($accident_id, 4);

        for my $address (@$address_list) {
          $Users->list({
            LOCATION_ID => $address->{address_id},
            COLS_NAME   => 1,
          });

          if ($Users->{TOTAL} > 0) {

            my $accident = $Accident->accident_info($accident_id);
            for my $user_ (@{$Users->{list}}) {
              accident_warning({
                UID  => $user_->{uid},
                TEXT => $accident->{descr},
              })
            }
          }
        }
      }
      return $html->redirect("?index=".get_function_index("accident_log"));
    }
  }

  my $select_priority = $html->form_select(
    'PRIORITY',
    {
      SELECTED   => $info->{priority} || 0,
      SORT_VALUE => 1,
      SORT_KEY   => 1,
      NO_ID      => 1,
      SEL_HASH   => \%priority,
    },
    { class => 'form-control' });

  my $select_admin = sel_admins({ SELECTED => $info->{aid} || $admin->{AID}, REQUIRED=>1 });


  my $end_date = $html->form_datetimepicker('END_TIME', $info->{end_time} || "$DATE $TIME");
  my $redate = $html->form_datetimepicker('REALY_TIME', $info->{realy_time} || "");

  my $keys = "city_name,district_name_check,street_name_check,number_check";

  my @selected_street = grep $_, map {  $_->{address_id} if($_->{type_id} == 3) } @$address_list;
  my @selected_district = grep $_, map { $_->{address_id} if($_->{type_id} == 1) } @$address_list;
  my @selected_build = grep $_, map {  $_->{address_id} if($_->{type_id} == 4) } @$address_list;
  my @selected_city = grep $_, map {  $_->{address_id} if($_->{type_id} == 2) } @$address_list;

  foreach my $build (@$builds_list) {
    next if (! $build->{district_name});
    my $street_id = $html->form_input('STREET', $build->{street_id}, {
      TYPE  => 'checkbox',
      STATE => in_array($build->{street_id}, \@selected_street),
      ID    => $build->{street_id},
    });

    $build->{street_name_check} = $html->element('label', $street_id . ' ' . ($build->{street_name} ? $build->{street_name} : ''));

    my $dist_id = $html->form_input('GEOLOCATION', $build->{district_id}, {
      TYPE  => 'checkbox',
      STATE => in_array($build->{district_id}, \@selected_district),
      ID    => $build->{district_id},
    });

    $build->{district_name_check} = $html->element('label', $dist_id . ' ' . ($build->{district_name} ? $build->{district_name} : '--'));

    my $number_id = $html->form_input('BUILD', $build->{location_id}, {
      TYPE  => 'checkbox',
      STATE => in_array($build->{location_id}, \@selected_build),
      ID    => $build->{location_id},
    });

    $build->{number_check} = $html->element('label', $number_id . " $lang{BUILD} " . ($build->{number} ? $build->{number} : ''));
    my $city_id = $html->form_input('CITY', $build->{city}, {
      TYPE  => 'checkbox',
      STATE => in_array($build->{city}, \@selected_city),
      ID    => $build->{city},
    });

    $build->{city_name} = $html->element('label', $city_id . ' ' . ($build->{city} ? $build->{city} : ''));
  }

  my $tree = $html->html_tree($builds_list, $keys, { OUTPUT2RETURN => 1 });

  my $date_today = $html->form_datetimepicker('DATE', $info->{date} || "$DATE $TIME");

  my $select_status = $html->form_select(
    'STATUS',
    {
      SELECTED       => $info->{status} || 0,
      SEL_HASH     => \%status,
      NO_ID        => 1,
      SORT_KEY_NUM => 1,
    },
    { class => 'form-control' }
  );

  $html->tpl_show(_include('accident_add', 'Accident'), {
    INDEX           => $index, #get_function_index('accident_log_add'),
    P_NAME          => $lang{NAME},
    NAME            => ($info->{name} ? lc $info->{name} : ''),
    DESCRIBE        => $lang{DESC},
    SELECT_PRIORITY => $select_priority,
    ADMIN_SELECT    => $select_admin,
    DATEPICKER_END  => $end_date,
    DATEPICKER_REAL => $redate,
    ADD             => !$FORM{change} ? $lang{ADD} : $lang{CHANGE},
    DESCR           => ($info->{descr} ? lc $info->{descr} : ''),
    ADD_CHG         => !$FORM{change} ? 'add' : 'chg',
    ID_CHANGE       => $info->{id},
    CHG_JS          => $FORM{ADDRESS_ID},
    GEO_TREE        => ($FORM{USER_ACCIDENT}) ? '' : $tree,
    SELECT_STATUS   => $select_status,
    DATE            => $date_today,
    USER_ACCIDENT   => $FORM{USER_ACCIDENT}
  });

  return 0;
}

#**********************************************************
=head2 accident_widget($attr)

  Arguments:
    -

  Returns:
    Table start page widget

=cut
#**********************************************************
sub accident_widget {
  require Admins;
  Admins->import();
  my $Admins = Admins->new($db, $admin, \%conf);

  $Address->build_list({
    LOCATION_ID   => '_SHOW',
    STREET_NAME   => '_SHOW',
    LOCATION_ID   => '_SHOW',
    CITY          => '_SHOW',
    DISTRICT_ID   => '_SHOW',
    DISTRICT_NAME => '_SHOW',
    NUMBER        => '_SHOW',
    STREET_ID     => '_SHOW',
    DOMAIN_ID     => $admin->{DOMAIN_ID},
    COLS_NAME     => 1,
  });

  my $admins_list = $Admins->list({
    LOGIN     => '_SHOW',
    AID       => '_SHOW',
    COLS_NAME => 1
  });

  my $accident_list_done = $Accident->list({
    AID        => '_SHOW',
    STATUS     => '_SHOW',
    NAME       => '_SHOW',
    END_TIME   => '_SHOW',
    REALY_TIME => '_SHOW',
    DATE       => '_SHOW',
    TYPE_ID    => '_SHOW',
    STATUS     => '_SHOW',
    DESCR      => '_SHOW',
    PRIORITY   => '_SHOW',
    DOMAIN_ID     => $admin->{DOMAIN_ID},
    COLS_NAME  => 1
  });

  my $table = $html->table(
    {
      width   => '100%',
      caption => $html->button($lang{ACCIDENT_LOG},
        'index=' . get_function_index('accident_log')),
      title   => [ $lang{NAME}, $lang{ADMIN}, $lang{WORK_END_DATE}, $lang{WORK_REALY_DATE}, $lang{STATUS} ],
      ID      => 'ACCIDENT',
    }
  );

  my $dublicate = 0;
  foreach my $element (@$accident_list_done) {
    my $admin_login = '';
    foreach my $admins (@$admins_list) {
      if ($element->{aid} == $admins->{aid}) {
        $admin_login = $admins->{login}
      }
    }

    next if ($dublicate == $element->{id});
    $dublicate = $element->{id};
    $table->addrow(
      $element->{name},
      $admin_login,
      $element->{end_time},
      $element->{realy_time},
      $html->color_mark($status{ $element->{status} }, $STATUSES_COLORS[ $element->{status} ])
    );
  }

  return $table->show();
}

#**********************************************************
=head2 accident_for_equipment()

  Arguments:
    -

  Returns:
    Table start page widget

=cut
#**********************************************************
sub accident_for_equipment {
  my $equipment_list = $Equipment->_list({
    NAS_ID    => '_SHOW',
    STATUS    => '_SHOW',
    NAS_NAME  => '_SHOW',
    NAS_DESCR => '_SHOW',
    PAGE_ROWS => 9999,
    COLS_NAME => 1
  });

  my $equipment_error = $Accident->accident_equipment_list({
    STATUS => 0,
    ID_EQUIPMENT => '_SHOW',
    PAGE_ROWS => 9999,
    COLS_NAME => 1
  });

  my @known_errors = map {$_->{id_equipment}} @$equipment_error;

  my $table = $html->table(
    {
      width   => '100%',
      caption => $html->button($lang{ACCIDENT_FOR_EQUIPMENT},
        'index=' . get_function_index('accident_equipment_log')),
      title   => [ $lang{EQUIPMENT}, $lang{DESCRIBE}, $lang{STATUS} ],
      ID      => 'ACCIDENT_EQUIPMENT',
    }
  );

  foreach my $equipment_element (@$equipment_list) {
    if ($equipment_element->{status} && $equipment_element->{status} == 3) {
      $table->addrow(
        $equipment_element->{nas_name} || '-',
        $equipment_element->{nas_descr} || '-',
        $lang{ERROR},
        !in_array($equipment_element->{nas_id}, \@known_errors) ?
        $html->button('', 'index=' . get_function_index('accident_equipment_log') .
          '&dash=1&add_form=' . $equipment_element->{nas_id}, { ICON => 'fa fa-plus' }) :
          $html->color_mark($lang{ADDED}, "#00cc00")
      );
    }
  }

  return $table->show();
}

#**********************************************************
=head2 accident_log_start_page($attr)

  Arguments:
    -

  Returns:
    Widget function

=cut
#**********************************************************
sub accident_start_page {
  my %START_PAGE_F = (
    'accident_widget' => $lang{ACCIDENT_LOG}
  );

  if (in_array('Equipment', \@MODULES) && $conf{EQUIPMENT_LOG}) {
    $START_PAGE_F{accident_for_equipment} = $lang{ACCIDETN_FOR_EQUIPMENT};
  }

  return \%START_PAGE_F;
}

#**********************************************************
=head2 accident_dashboard_mess()

=cut
#**********************************************************
sub accident_dashboard_mess {
  my $user_date = $user->pi({
    UID => $user->{UID},
  });

  if ($user_date->{LOCATION_ID}) {
    my $user_location = $Address->build_list({
      LOCATION_ID => $user_date->{LOCATION_ID},
      DOMAIN_ID   => '_SHOW',
      COLS_NAME   => 1,
      DISTRICT_ID => '_SHOW',
    });

    my $accident_list = $Accident->list({
      DISTRICT_ID => $user_location->[0]->{district_id},
      STREET_ID   => $user_location->[0]->{street_id},
      BUILD_ID    => $user_location->[0]->{location_id},
      DOMAIN_ID   => $user_location->[0]->{domain_id},
      ADDRESS_ID  => '_SHOW',
      STATUS      => '0',
      TYPE_ID     => '_SHOW',
      DESCR       => '_SHOW',
      COLS_NAME   => 1
    });

    if($accident_list){
      for my $accident (@$accident_list){
        $html->tpl_show(_include('accident_user_warr', 'Accident'), {
          MESSAGE => $accident->{descr},
          END_DATE => $accident->{end_time}
        });
      }
    }

  }

  if (in_array('Equipment', \@MODULES) && $conf{EQUIPMENT_LOG}) {
    my $warning_info = $Accident->accident_equipment_list({
      ID_EQUIPMENT => '_SHOW',
      END_DATE     => '_SHOW',
      NAS_ID       => '_SHOW',
      STATUS       => '0',
      UID          => $user->{UID},
      EXT_TABLE    => 1,
      COLS_NAME    => 1,
    });

    if($warning_info){
      for my $accident (@$warning_info){
        $html->tpl_show(_include('accident_user_warr', 'Accident'), {
          MESSAGE => $lang{EQUIPMENT_ERR},
          END_DATE => $accident->{end_date},
        });
      }
    }

  }


  return 0;
}

#**********************************************************
=head2 accident_equipment_log()

  Arguments:


  Returns:

=cut
#**********************************************************
sub accident_equipment_log {
  if ($FORM{del} && $FORM{COMMENTS}) {
    $Accident->accident_equipment_del({ ID => $FORM{del} });

    if (!_error_show($Accident)) {
      $html->message('success', $lang{SUCCESS});
    }
    else {
      $html->message('err', $lang{ERROR});
    }
  }

  if ($FORM{add_form} || $FORM{add} || $FORM{chg} || $FORM{change}) {
    _equipment_error({ %FORM });
  }

  my $index = get_function_index('accident_equipment_log');
  my $admins_list = sel_admins({ HASH => 1 });
  my $table = $html->table({
    width   => '100%',
    caption => $lang{ACCIDENT_FOR_EQUIPMENT},
    title   => [ 'ID', $lang{NAME}, $lang{DATE}, $lang{STATUS}, $lang{RESPONSIBLE} ],
    ID      => 'ACCIDENT_EQUIPMENT',
  });

  my $list_equipment_error = $Accident->accident_equipment_list({
    ID_EQUIPMENT => '_SHOW',
    DATE         => '_SHOW',
    END_DATE     => '_SHOW',
    STATUS       => '_SHOW',
    AID          => '_SHOW',
    COLS_NAME    => 1,
  });

  my $equipment_list = $Equipment->_list({
    NAS_ID    => '_SHOW',
    NAS_NAME  => '_SHOW',
    PAGE_ROWS => 9999,
    COLS_NAME => 1
  });

  my %nas_name = map {$_->{nas_id} => $_->{nas_name}} @{$equipment_list};

  foreach my $equipment_element (@$list_equipment_error) {
    my $delete = $html->button($lang{DEL},
      "index=$index&del=$equipment_element->{id}",
      { MESSAGE => "$lang{DEL} [$equipment_element->{id}] ?", ICON => 'fa fa-trash', class => 'text-danger' });

    my $change = $html->button($lang{CHANGE},
      "index=$index&change=$equipment_element->{id}&actions=1",
      { ICON => 'fa fa-pencil' });

    $table->addrow(
      $equipment_element->{id},
      $nas_name{ $equipment_element->{id_equipment} },
      "$equipment_element->{date} - $equipment_element->{end_date}",
      $html->color_mark($status{ $equipment_element->{status} }, $STATUSES_COLORS[ $equipment_element->{status} ]),
      $admins_list->{ $equipment_element->{aid} },
      $change,
      $delete,
    );
  }

  print $table->show();

  return 1;
}

#**********************************************************
=head2 _equipment_error()

  Arguments:


  Returns:

=cut
#**********************************************************
sub _equipment_error {
  my ($attr) = @_;
  my %tpl_info = ();

  if ($attr->{dash} || $attr->{change}) {
    if ($attr->{change}) {
      my $info_accident = $Accident->accident_equipment_info($attr->{change});

      $tpl_info{FROM_DATE} = $info_accident->[0]{date};
      $tpl_info{TO_DATE} = $info_accident->[0]{end_date};
      $tpl_info{AID} = $info_accident->[0]{aid};
      $tpl_info{STATUS} = $info_accident->[0]{status};
      $tpl_info{ID_EQUIPMENT} = $info_accident->[0]{id_equipment};
      $tpl_info{chg} = $attr->{change};
    }

    my $info = $Equipment->_list({
      NAS_ID    => $attr->{add_form} || $tpl_info{ID_EQUIPMENT},
      NAS_NAME  => '_SHOW',
      COLS_NAME => 1
    });

    $tpl_info{id_equipment} = $attr->{add_form};
    $tpl_info{NAME} = $info->[0]{nas_name};
  }

  if ($attr->{add}) {
    $Accident->accident_equipment_add({
      ID_EQUIPMENT => $attr->{id_equipment},
      DATE         => $attr->{FROM_DATE},
      END_DATE     => $attr->{TO_DATE},
      AID          => $attr->{AID},
      STATUS       => $attr->{ACCIDENT_EQUIPMENT_ID}
    });

    if (!_error_show($Accident)) {
      $html->message('success', $lang{ADDED});

      if ($conf{ACCIDENT_WARNING}) {
        my $Internet = Internet->new($db, $admin, \%conf);
        my $list = $Internet->user_list({
          NAS_ID    => $attr->{id_equipment},
          COLS_NAME => 1,
        });
        if ($Internet->{TOTAL} > 0) {
          for my $user_ (@$list) {
            accident_warning({
              UID  => $user_->{uid},
              TEXT => $lang{EQUIPMENT_ERR},
            })
          }
        }
      }
    }
    else {
      $html->message('err', $lang{ERROR});
    }
  }

  if ($attr->{chg}) {
    $Accident->accident_equipment_chg({
      ID       => $attr->{chg},
      AID      => $attr->{AID},
      DATE     => $attr->{FROM_DATE},
      END_DATE => $attr->{TO_DATE},
      STATUS   => $attr->{ACCIDENT_EQUIPMENT_ID}
    });

    if (!_error_show($Accident)) {
      $html->message('success', $lang{CHANGE});
    }
    else {
      $html->message('err', $lang{ERROR});
    }
  }

  _show_equipment_error({
    %tpl_info,
    add_form              => $attr->{add_form},
    ACCIDENT_EQUIPMENT_ID => $attr->{ACCIDENT_EQUIPMENT_ID}
  });

  return 1;
}

#**********************************************************
=head2 _show_equipment_error()

  Arguments:


  Returns:

=cut
#**********************************************************
sub _show_equipment_error {
  my ($attr) = @_;

  my $admins_list = sel_admins({
    SELECTED => $attr->{AID}
  });

  my $status_sel = $html->form_select(
    'ACCIDENT_EQUIPMENT_ID',
    {
      SELECTED    => $attr->{ACCIDENT_EQUIPMENT_ID} || $attr->{STATUS} || 0,
      SEL_HASH    => \%status,
      NO_ID       => 1,
      SEL_OPTIONS => { 0 => '--' },
    }
  );

  $html->tpl_show(_include('accident_equipment_add', 'Accident'), {
    %$attr,
    INDEX         => $index,
    BUTTON_ACTION => $attr->{add_form} ? $lang{ADD} : $lang{CHANGE},
    RESPONSIBLE   => $admins_list,
    STATUS        => $status_sel,
    add           => $attr->{add_form} ? 1 : 0,
  });

  return 1;
}

#**********************************************************
=head2 accident_compensation()

  Arguments:


  Returns:

=cut
#**********************************************************
sub accident_compensation {
  my $index = get_function_index('accident_compensation');
  my $services = sel_tp();

  my $accident_info = $Accident->accident_address_info($FORM{ID});

  my $type_id = $accident_info->[0]{type_id};
  my $address_id = $accident_info->[0]{address_id};
  my $location_info = _search_address($type_id, $address_id);

  my %EXT_TITLES = (
    'id'          => '#',
    'procent'     => $lang{PROCENT},
    'date'        => $lang{DATE},
    'service'     => $lang{SERVICE},
    'address_id'  => $lang{ADDRESS},
    'ADDRESS_IDS' => $lang{ADDRESS}
  );

  my $service = $html->form_select(
    'SERVICE',
    {
      SELECTED     => $FORM{SERVICE} || '',
      SEL_HASH     => $services,
      SORT_KEY_NUM => 1,
      NO_ID        => 1
    }
  );

  my $address = form_address_select2({
    DISTRICT_ID => $location_info->{DISTRICT} || 0,
    STREET_ID   => $location_info->{STREET} || 0,
    BUILD_ID    => $location_info->{BUILD} || 0,
    HIDE_FLAT   => 1
  });

  my $build_list_address = $Address->build_list({
    LOCATION_ID   => $FORM{LOCATION_ID} || '_SHOW',
    STREET_ID     => $FORM{STREET_ID} || '_SHOW',
    DISTRICT_ID   => $FORM{DISTRICT_ID} || '_SHOW',
    DOMAIN_ID     => $admin->{DOMAIN_ID},
    STREET_NAME   => '_SHOW',
    CITY          => '_SHOW',
    DISTRICT_NAME => '_SHOW',
    NUMBER        => '_SHOW',
    USERS         => '_SHOW',
    COLS_NAME     => 1,
  });

  if ($FORM{COMPENSATION}) {
    _accident_add_compensation($build_list_address, {
      SERVICE => $FORM{SERVICE},
      type_id => $FORM{type_id},
      address => $FORM{address},
      ID      => $FORM{ID},
    });
  }

  if ($FORM{del} && $FORM{COMMENTS}) {
    $Accident->accident_compensation_del({ ID => $FORM{del} });
  }

  if ($FORM{compensation}) {
    $html->tpl_show(_include('accident_compensation', 'Accident'), {
      INDEX        => $index,
      TYPE_ID      => $location_info->{TYPE_ID} || 0,
      ADDRESS      => $location_info->{DISTRICT} || $location_info->{STREET} || $location_info->{BUILD} || 0,
      SERVICE      => $service,
      ADDRESS_FORM => $address,
      ID           => $FORM{ID}
    });
  }

  result_former({
    INPUT_DATA      => $Accident,
    FUNCTION        => 'accident_compensation_list',
    DEFAULT_FIELDS  => 'ID,PROCENT,DATE,SERVICE',
    HIDDEN_FIELDS   => 'ADDRESS_ID',
    FUNCTION_FIELDS => 'del',
    SKIP_USER_TITLE => 1,
    EXT_TITLES      => \%EXT_TITLES,
    FILTER_VALUES   => {
      procent => sub {
        my ($procent) = @_;
        return "$procent%"
      },
      service => sub {
        my ($id_service) = @_;

        return $services->{$id_service};
      },
    },
    TABLE           => {
      width   => '100%',
      caption => $lang{COMPENSATION},
    },
    MAKE_ROWS       => 1,
    TOTAL           => 1
  });

  return 1;
}

#**********************************************************
=head2 _accident_add_compensation()

  Arguments:


  Returns:

=cut
#**********************************************************
sub _accident_add_compensation {
  my ($build_list_address, $attr) = @_;

  return 0 if (!in_array('Internet', \@MODULES));

  require Internet;
  Internet->import();
  my $Internet = Internet->new($db, $admin, \%conf);

  my $day = $Accident->accident_date_compensation($attr->{ID});
  my $money_compensation = 0;

  my $error_uid = '';

  my $tp_info = $Internet->user_list({
    TP_ID     => $attr->{SERVICE},
    DAY_FEE   => '_SHOW',
    MONTH_FEE => '_SHOW',
    COLS_NAME => 1
  });

  my @users_uid = ();
  foreach my $user_uid (@$build_list_address) {
    if ($user_uid->{users}) {
      if ($user_uid->{users} =~ /,/) {
        push @users_uid, split(/,/, $user_uid->{users});
      }
      else {
        push @users_uid, $user_uid->{users};
      }
    }
  }

  if ($tp_info->[0]->{month_fee} || $tp_info->[0]->{day_fee}) {
    if ($tp_info->[0]->{month_fee}) {
      my @day_count = ();

      push @day_count, localtime(timelocal(undef, undef, undef, 1, $_, undef))->month_last_day for 0 .. 11;

      my $month = strftime("%m", localtime(time));
      my $count_month = $day_count[ $month - 1 ];

      $money_compensation = ($tp_info->[0]->{month_fee} / $count_month) * $day->[0];
    }
    elsif ($tp_info->[0]->{day_fee}) {
      $money_compensation = $tp_info->[0]->{day_fee} * $day->[0];
    }
    else {
      return 0;
    }
  }

  $Accident->accident_compensation_add({
    PROCENT    => $money_compensation,
    DATE       => 'NOW()',
    SERVICE    => $attr->{SERVICE},
    TYPE_ID    => $attr->{type_id},
    ADDRESS_ID => $attr->{address},
  });

  if (!_error_show($Accident)) {
    require Payments;
    Payments->import();

    my $Payments = Payments->new($db, $admin, \%conf);

    foreach my $uid (@users_uid) {
      my $user = $Users->info($uid);
      $Payments->add($user, {
        SUM    => $money_compensation,
        METHOD => 6
      });

      if (_error_show($Payments)) {
        $error_uid .= "$uid, ";
      }
    }

    if ($error_uid eq '') {
      $html->message('success', $lang{SUCCESS}, $lang{ADDED});
    }
    else {
      $html->message('err', $lang{ERROR}, "UID: $error_uid");
    }
  }
}
#**********************************************************
=head2 accident_compensation()

  Arguments:


  Returns:

=cut
#**********************************************************
sub _search_address {
  my ($type_id, $address_id) = @_;

  my %location_info = ();

  return \%location_info unless ($type_id && $address_id);

  if ($type_id == 1) {
    $location_info{DISTRICT} = $address_id;
    $location_info{TYPE_ID} = 1;
  }
  elsif ($type_id == 2) {
    $location_info{CITY} = $address_id;
    $location_info{TYPE_ID} = 2;
  }
  elsif ($type_id == 3) {
    $location_info{STREET} = $address_id;
    $location_info{TYPE_ID} = 3;
  }
  elsif ($type_id == 4) {
    $location_info{BUILD} = $address_id;
    $location_info{TYPE_ID} = 4;
  }

  return \%location_info;
}

#**********************************************************
=head2 accident_element_add()

  Arguments:


  Returns:

=cut
#**********************************************************
sub accident_element_add {
  my ($attr) = @_;

  foreach my $element (@{$attr->{ADDRESS}}) {
    $Accident->address_add({
      AC_ID      => $attr->{INSERT_ID},
      TYPE_ID    => $attr->{TYPE_ID},
      ADDRESS_ID => $element,
    });
  }

}

#**********************************************************
=head2 accident_user()

  Arguments:


  Returns:

=cut
#**********************************************************
sub accident_user {
  my ($attr) = @_;

  my $admins_list = sel_admins({ HASH => 1 });

  my $user_accident_list = $Accident->user_accident_list({
    UID       => $attr->{USER_INFO}->{UID},
    COLS_NAME => 1
  });

  my $table = $html->table(
    {
      width   => '100%',
      caption => "$lang{ACCIDENT_LOG} - $lang{USER}",
      title   => [ $lang{NAME}, $lang{DESCRIBE}, $lang{ADMIN}, $lang{STATUS} ],
      ID      => 'ACCIDENT_USER',
    }
  );

  my $size = @{$user_accident_list};

  unless ($size) {
    $html->message('info', $lang{ACCIDENT_LOG}, $lang{USER_ACCIDENT_EMPTY});

    return 1;
  }

  foreach my $user_accident (@$user_accident_list) {
    my $button_chg = $html->button($lang{CHANGE}, "&change=$user_accident->{id}&index=" . get_function_index('accident_log_add'),
      { class => 'change' });

    $table->addrow(
      $user_accident->{name},
      $user_accident->{descr},
      $admins_list->{ $user_accident->{aid} },
      $status{ $user_accident->{status} },
      $button_chg
    );
  }

  print $table->show();

  return 0;
}

#**********************************************************
=head2 accident_quick_info($attr) - Quick information

  Arguments:
    $attr

=cut
#**********************************************************
sub accident_quick_info {
  my ($attr) = @_;
  my $uid = $attr->{UID} || $FORM{UID};

  return () if ($FORM{sid});

  if ($attr->{UID}) {
    my $list = $Accident->user_accident_list({
      UID       => $uid,
      COLS_NAME => 1
    });

    my @result = ();
    my $admins_list = sel_admins({ HASH => 1 });

    foreach my $line (@$list) {

      push @result, {
        NAME   => $line->{name},
        DESC   => $line->{descr},
        STATUS => $status{ $line->{status} },
        AID    => $admins_list->{ $line->{aid} },
      };
    }

    return \@result;
  }
  elsif ($attr->{GET_PARAMS}) {
    my %result = (
      HEADER => $lang{ACCIDENT_LOG},
      SLIDES => [
        { NAME => $lang{NAME} },
        { DESC => $lang{DESCRIBE} },
        { STATUS => $lang{STATUS} },
        { AID => $lang{RESPONSIBLE} },
      ]
    );

    return \%result;
  }

  $Accident->user_accident_list({
    UID       => $uid,
    COLS_NAME => 1
  });

  return ($Accident->{TOTAL} > 0) ? $Accident->{TOTAL} : '';
}


#**********************************************************
=head2 accident_warning($attr) - Send warning to telegram or viber

  Arguments:
    $attr
      UID
      TEXT

=cut
#**********************************************************
sub accident_warning {
  my ($attr) = @_;

  my @types = split ',', $conf{ACCIDENT_WARNING};

  my $message = $lang{WARNING} . " \n";
  $message .= $attr->{TEXT};

  for my $type (@types) {
    my $responce = $Sender->send_message({
      UID         => $attr->{UID},
      MESSAGE     => $message,
      SENDER_TYPE => $type,
    });

    last if ($responce);
  }

  return 1;
}

#**********************************************************
=head2 accident_auto_close($attr) - auto close accident (periodic)

=cut
#**********************************************************
sub  accident_auto_close{
  my ($attr) = @_;
  my $debug = $attr->{DEBUG} || 0;

  return 0 if(!$DATE);

  $Accident->{debug} = $debug;

  my $list = $Accident->list({
    STATUS   => 0,
    END_TIME => "<$DATE",
    COLS_NAME => 1,
  });


  if($Accident->{TOTAL} > 0) {
    foreach (@$list) {
      print "Close $_->{name} ($_->{id})\n" if ($debug);
      $Accident->change_element({
        ID     => $_->{id},
        STATUS => 2
      });
    }
  }
  else {
    print "NO Accidents to close\n" if($debug);
  }


  return 1;
}

1;
