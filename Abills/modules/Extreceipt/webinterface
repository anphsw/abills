#!perl
=head1 NAME

 Extreceipts web functions

=cut

use strict;
use warnings FATAL => 'all';

use Extreceipt::db::Extreceipt;
use Extreceipt::Base;
use Reports;
use Conf;


our (
  $index,
  %lang,
  $admin,
  $db,
  %conf,
);

our Abills::HTML $html;

my $Receipt = Extreceipt->new($db, $admin, \%conf);
my $Config = Conf->new($db, $admin, \%conf);

#**********************************************************
=head2 extreceipt_api()

=cut
#**********************************************************
sub extreceipt_api {
  my $admin_list = '';

  $Receipt->{ACTION}='add';
  $Receipt->{LNG_ACTION}=$lang{ADD};

  if ($FORM{del} && $FORM{COMMENTS}) {
    $Receipt->api_del($FORM{del});
    $html->message('info', $lang{SUCCESS}) if (_error_show($Receipt));
  }
  elsif ($FORM{change}) {
    $admin_list = $admin->list({AID => $FORM{AID}, COLS_NAME => 1 });

    $FORM{AUTHOR} = $admin_list->[0]->{name};

    $Receipt->api_change({ %FORM, API_ID => $FORM{chg} });
    $html->message('info', $lang{SUCCESS}) if (_error_show($Receipt));
  }
  elsif ($FORM{chg}) {
    my $api_info = $Receipt->api_list({ API_ID => $FORM{chg} });

    %FORM = %{ $api_info->[0] };
    $FORM{add_form}=1;
    $FORM{ID}=$FORM{API_ID};
    $FORM{ACTION}='change';
    $FORM{LNG_ACTION}=$lang{CHANGE};
  }
  elsif ($FORM{add}) {
    $admin_list = $admin->list({AID => $FORM{AID}, COLS_NAME => 1 });
    $FORM{AUTHOR} = $admin_list->[0]->{name};

    $Receipt->api_add(\%FORM);
    $html->message('info', $lang{SUCCESS}) if (_error_show($Receipt));
  }
  elsif ($FORM{test}) {
    $html->message('info', $lang{TEST});
    my $Receipt_apies = receipt_init($Receipt, { API => $FORM{test} });

    my $Receip_api = $Receipt_apies->{$FORM{test}};

    if ($Receip_api->can('test')) {
      $Receip_api->test();
      if ($Receip_api->{error}) {
        _error_show($Receip_api);
      }
      else {
        my $table = $html->table({
          width   => '100%',
          caption => 'Test',
          ID      => 'TEST_API',
        });

        foreach my $key (keys %{$Receip_api->{test_result}}) {
          my $value = ($Receip_api->{test_result}->{$key}) ? $Receip_api->{test_result}->{$key} : q{};
          Encode::_utf8_off($value);
          $table->addrow($key, $value);
        }

        print $table->show();
      }
    }
  }

  if ($FORM{add_form}) {
    $FORM{AID} = sel_admins({
      NAME => 'AID',
      SELECTED => $Receipt->{AID} || $FORM{AID}
    });

    $html->tpl_show(
      _include('receipts_api', 'Extreceipt'),
      {
        %$Receipt,
        %FORM,
      }
    );
  }

  $Receipt->{debug}=1;
  my $api_list = $Receipt->api_list();

  my $table = $html->table({
    width               => '100%',
    caption             => "Receipts API : ",
    ID                  => 'API',
    title               => [ "#", "CONF_NAME", "API plugin", "Autor", "" ],
    HAS_FUNCTION_FIELDS => 1,
    MENU                => "$lang{ADD}:index=$index&add_form=1:add"
  });

  foreach my $api (@$api_list) {
    $table->addrow($api->{api_id},
      $api->{conf_name},
      $api->{api_name},
      $api->{author},
      $html->button($lang{CHANGE}, "index=$index&chg=$api->{api_id}", { class => 'change' })
      . $html->button($lang{DELETE}, "index=$index&del=$api->{api_id}", { class => 'del', MESSAGE => $lang{DEL} })
      . $html->button($lang{TEST}, "index=$index&test=$api->{api_id}", { class => 'test' })
    );
  }
  print $table->show();

  return 1;
}

#**********************************************************
=head2 extreceipt_kkt()

=cut
#**********************************************************
sub extreceipt_kkt {

  my %params = (
    ACTION => 'add',
    LNG_ACTION   => $lang{ADD},
  );

  if ($FORM{add}) {
    my $api_list = $Receipt->api_list({ API_ID => $FORM{API_ID}, COLS_NAME => 1 });
    $FORM{ADMINS} = $api_list->[0]->{AUTHOR};

    $Receipt->kkt_add(\%FORM);
    $html->message('info', $lang{SUCCESS}) if (_error_show($Receipt));
  }
  elsif ($FORM{change}) {
    $Receipt->kkt_change({ %FORM, KKT_ID => $FORM{chg} });
    $html->message('info', $lang{SUCCESS}) if (_error_show($Receipt));
  }
  elsif ($FORM{chg}) {
    my $kkt_info = $Receipt->kkt_list({ KKT_ID => $FORM{chg} });

    %params = %{ $kkt_info->[0] };
    $FORM{add_form}=1;
    $params{ID}=$params{KKT_ID};
    $params{ACTION}='change';
    $params{LNG_ACTION}=$lang{CHANGE};
  }
  elsif ($FORM{del} && $FORM{COMMENTS}) {
    $Receipt->kkt_del($FORM{del});
    $html->message('info', $lang{SUCCESS}) if (_error_show($Receipt));
  }

  if ($FORM{add_form}) {
    $params{API_SEL}=$html->form_select('API_ID', {
      SEL_LIST => $Receipt->api_list(),
      SELECTED => $params{api_id},
      SEL_KEY  => 'api_id',
      SEL_VALUE=> 'api_name,conf_name'
    });

    $params{ADMINS_SEL} = sel_admins({
      NAME => 'AID',
      SELECTED => $params{AID} || $FORM{AID}
    });

    $html->tpl_show('', \%params,
      { TPL => 'receipts_kkt', MODULE => 'Extreceipt' });
  }

  my $kkt_list = $Receipt->kkt_list();

  my $table = $html->table({
    width               => '100%',
    caption             => "KKT",
    ID                  => 'KKT',
    title               => [ "#", "API name", "KKT $lang{GROUP}", $lang{PAYMENT_METHOD}, $lang{GROUPS}, $lang{ADMIN}, "" ],
    HAS_FUNCTION_FIELDS => 1,
    MENU                => "$lang{ADD}:index=$index&add_form=1:add"
  });

  foreach my $kkt (@$kkt_list) {
    $table->addrow(
      $kkt->{kkt_id},
      $kkt->{api_name},
      $kkt->{kkt_group},
      $kkt->{methods},
      $kkt->{groups},
      $kkt->{aid},
      $html->button($lang{CHANGE}, "index=$index&chg=$kkt->{kkt_id}", { class => 'change' })
      . $html->button($lang{DELETE}, "index=$index&del=$kkt->{kkt_id}", { class => 'del', MESSAGE => $lang{DEL} })
    );
  }
  print $table->show();

  return 1;
}

#**********************************************************
=head2 extreceipt_check_list()

=cut
#**********************************************************
sub extreceipt_check_list {
  my $Receipt_api = receipt_init($Receipt);
  $Receipt->{API} = $Receipt_api;

  if ($FORM{new}) {
    _extreceipt_new();
  }
  elsif ($FORM{send}) {
    _extreceipt_send($FORM{send}, { API => $Receipt->{API} });
  }
  elsif ($FORM{info}) {
    _extreceipt_info($FORM{info});
  }
  elsif ($FORM{undo}) {
    if (_extreceipt_undo($FORM{undo})) {
      $html->message('info', $lang{INFO}, $lang{DELETED});
    }
  }
  elsif ($FORM{resend}) {
    _extreceipt_resend($FORM{resend});
  }

  if (!$FORM{sort}) {
    $LIST_PARAMS{SORT} = 1;
    $LIST_PARAMS{DESC} = 'DESC';
  }

  my $PAYMENTS_METHODS = get_payment_methods();

  my $payment_method_select = $html->form_select('PAYMENT_METHOD', {
    SELECTED => $FORM{PAYMENT_METHOD} || '*',
    SEL_HASH => { '*' => $lang{ALL}, %$PAYMENTS_METHODS },
    NO_ID    => 1,
    SORT_KEY => 1
  });

  my %receipt_status = (
    1 => $lang{SENDED},
    2 => $lang{ACCEPTED},
    3 => $lang{CANCELED},
    4 => $lang{ERROR},
  );

  my $receipt_status_select = $html->form_select('STATUS', {
    SELECTED => $FORM{STATUS} || '*',
    SEL_HASH => { '*' => $lang{ALL}, '<>2' => "$lang{NOT} $lang{ACCEPTED}", %receipt_status },
    NO_ID    => 1,
    SORT_KEY => 1
  });

  reports({
    # PERIODS     => 1,
    NO_TAGS     => 1,
    NO_GROUP    => 1,
    PERIOD_FORM => 1,
    EXT_SELECT  => {
      PAYMENT_METHOD => { LABEL => $lang{PAYMENT_METHOD}, SELECT => $payment_method_select },
      RECEIPT_STATUS => { LABEL => $lang{STATUS}, SELECT => $receipt_status_select },
    },
    EX_INPUTS   => [
      "$lang{ROWS}: " . $html->form_input( 'PAGE_ROWS', $html->{PAGE_ROWS},
        { OUTPUT2RETURN => 1 } ),
      "$lang{DEBUG}: " . $html->form_input( 'DEBUG', 1,
        { OUTPUT2RETURN => 1, TYPE => 'checkbox', STATE => $FORM{DEBUG} } ),
    ]
  });

  if ($FORM{DEBUG}) {
    $Receipt->{debug}=1;
  }

  my @params = ('PAYMENT_METHOD', 'STATUS');
  foreach my $k (@params) {
    if ($FORM{$k}) {
      $LIST_PARAMS{$k} = $FORM{$k};
      $pages_qs .= "&$k=$FORM{$k}";
    }
  }

  result_former({
    INPUT_DATA     => $Receipt,
    FUNCTION       => 'payments_list',
    DEFAULT_FIELDS => 'ID,DATETIME,LOGIN,SUM,PAYMENT_METHOD,STATUS,EXTST',
    FILTER_COLS    => {
      extst          => '_extreceipt_buttons_filter::id,extst,payment_method',
      payment_method => '_extreceipt_payment_filter::',
    },
    SELECT_VALUE   => {
      status => {
        0 => $lang{NEW},
        1 => $lang{SENDED},
        2 => $lang{ACCEPTED},
        3 => $lang{CANCELED},
        4 => $lang{ERROR},
      }
    },
    FUNCTION_INDEX => $index,
    EXT_TITLES     => {
      'id'             => $lang{NUM},
      'datetime'       => $lang{DATE},
      'sum'            => $lang{SUM},
      'payment_method' => $lang{PAYMENT_METHOD},
      'status'         => $lang{STATUS},
      'extst'          => $lang{EXIST}
    },
    TABLE          => {
      width   => '100%',
      caption => $lang{PAYMENTS},
      qs      => $pages_qs,
      pages   => $Receipt->{TOTAL},
      ID      => 'EXTRECEIPT_PAYMENTS',
    },
    MAKE_ROWS      => 1,
    SEARCH_FORMER  => 1,
    TOTAL          => 1
  });

  return 1;
}

#**********************************************************
=head2 _extreceipt_new()

=cut
#**********************************************************
sub _extreceipt_new {
  my $start_id = $conf{EXTRECEIPT_LAST_ID};

  $Receipt->get_new_payments($start_id);

  if ($Receipt->{LAST_ID}) {
    $Config->config_add({
      PARAM   => "EXTRECEIPT_LAST_ID",
      VALUE   => $Receipt->{LAST_ID},
      REPLACE => 1
    });
    $conf{EXTRECEIPT_LAST_ID} = $Receipt->{LAST_ID};
  }

  return 1;
}

#**********************************************************
=head2 _extreceipt_send($payment_id)

  Arguments:
    $payment_id -

  Return:
    TRUE or FALSE

=cut
#**********************************************************
sub _extreceipt_send {
  my ($payment_id, $attr) = @_;

  my $info = $Receipt->info($payment_id);

  if (! $Receipt->{TOTAL}) {

    return 0;
  }

  if (! $attr->{API}) {
    $Receipt->{API} = receipt_init($Receipt);
  }

  my $api_id = $info->[0]->{api_id} || 0;

  if (! $Receipt->{API}->{$api_id}) {
    $html->message('err', $lang{ERROR}, "$api_id NOT_ACTIVATE");
    return 0;
  }


  # $info->[0]->{phone} //= '';
  # $info->[0]->{phone} =~ s/\s+//g;
  # $info->[0]->{phone} =~ s/\W+//g;
  # $info->[0]->{mail} //= ($info->[0]->{uid} . '@myISP.ru');

  my $command_id = $Receipt->{API}->{$api_id}->payment_register($info->[0]);
  if ($command_id) {
    $Receipt->change({
      PAYMENTS_ID => $info->[0]->{payments_id},
      COMMAND_ID  => $command_id,
      STATUS      => 1
    });
  }

  return 1;
}

#**********************************************************
=head2 _extreceipt_info($id)

  Arguments:
    $id - Receipt id

=cut
#**********************************************************
sub _extreceipt_info {
  my ($id) = @_;

  my $info = $Receipt->info($id);
  my ($fdn, $fda, $date, $payments_id, $error) = $Receipt->{API}->{$info->[0]->{api_id}}->get_info($info->[0]);

  if ($error) {
    $Receipt->change({
      PAYMENTS_ID => $payments_id || $info->[0]->{command_id},
      STATUS      => 4,
    });
  }
  elsif ($fda) {
    $date =~ s/T/ /;
    $date =~ s/\+.*//;
    $Receipt->change({
      PAYMENTS_ID  => $payments_id || $info->[0]->{command_id},
      FDN          => $fdn,
      FDA          => $fda,
      RECEIPT_DATE => $date,
      STATUS       => 2,
    });
  }
  else {
    $html->message('info', "FDN = $fdn, FDA = $fda");
  }

  return 1;
}

#**********************************************************
=head2 _extreceipt_undo($payment_id)

  Arguments:
    $payment_id

  Results:


=cut
#**********************************************************
sub _extreceipt_undo {
  my ($payment_id) = @_;

  my $info = $Receipt->info($payment_id);
  my $command_id = $Receipt->{API}->{$info->[0]->{api_id}}->payment_cancel($info->[0]);

  if ($command_id) {
    $Receipt->change({
      PAYMENTS_ID => $payment_id,
      CANCEL_ID => $command_id,
      STATUS => 3
    });
    return 1;
  }

  return 0;
}

#**********************************************************
=head2 _extreceipt_resend()

=cut
#**********************************************************
sub _extreceipt_resend {
  my ($payment_id) = @_;
  $html->message('warning', $payment_id);
  return 1;
}

#**********************************************************
=head2 _extreceipt_buttons_filter(undef, $values)

=cut
#**********************************************************
sub _extreceipt_buttons_filter {
  my (undef, $values) = @_;

  my $attr = $values->{VALUES};
  my $buttons = '';

  if ($attr->{id} > $conf{"EXTRECEIPT_LAST_ID"}) {
    $buttons = $html->button($lang{NEW}, "index=$index&new=1&$pages_qs", { class => 'btn btn-xs btn-secondary' });
  }
  elsif (!defined($attr->{extst})) {
    return '';
  }
  elsif (!$attr->{extst}) {
    $buttons = $html->button($lang{SEND}, "index=$index&send=$attr->{id}&$pages_qs", { class => 'btn btn-xs btn-success' });
  }
  elsif ($attr->{extst} == 1) {
    $buttons = $html->button($lang{INFO}, "index=$index&info=$attr->{id}&$pages_qs", { class => 'btn btn-xs btn-info' });
  }
  elsif ($attr->{extst} == 2) {
    $buttons = $html->button($lang{UNDO}, "index=$index&undo=$attr->{id}&$pages_qs", { class => 'btn btn-xs btn-danger' });
  }
  elsif ($attr->{extst} == 3) {
    # Чек отменен
  }
  elsif ($attr->{extst} == 4) {
    $buttons = $html->button($lang{RESEND}, "index=$index&resend=$attr->{id}&$pages_qs", { class => 'btn btn-xs btn-success' });
  }
  elsif ($attr->{extst} == 5) {
    $buttons = $html->button($lang{RESEND}, "index=$index&resend=$attr->{id}&$pages_qs", { class => 'btn btn-xs btn-success' });
  }

  return $buttons;
}

#**********************************************************
=head2 _extreceipt_payment_filter()

=cut
#**********************************************************
sub _extreceipt_payment_filter {
  my ($method) = @_;
  return '' unless $method;

  my $PAYMENTS_METHODS = get_payment_methods();

  return $PAYMENTS_METHODS->{$method};
}

#**********************************************************
=head2 extreceipt_payments_maked($attr) - Cross module payment maked

  Arguments:
    $attr
      USER_INFO
      SUM

=cut
#**********************************************************
sub extreceipt_payments_maked {
  my ($attr) = @_;

  _extreceipt_new();
  _extreceipt_send($attr->{PAYMENT_ID});

  return 1;
}


1;