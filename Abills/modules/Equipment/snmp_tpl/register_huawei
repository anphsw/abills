#!/usr/bin/perl -w
=head1 NAME

 HUAWEI Configuration utility

=head2 EXAMLES

interface gpon 0/0
ont confirm 0 sn-auth 485754438082017B omci ont-lineprofile-id 100 ont-srvprofile-id 100 desc "TEST"
ont port native-vlan 1 0 eth 1 vlan 101 priority 0
quit
service-port vlan 13 gpon 0/1/1 ont 0 gemport 1 multi-service user-vlan 101  tag-transform translate

=cut


use strict;
BEGIN {
  use FindBin '$Bin';
  our $libpath = $Bin . '/../../../../';
  unshift( @INC,
    $libpath . 'lib/',
    $libpath,
    $libpath . 'Abills/',
    $libpath . 'Abills/mysql/',
  );

}
use Abills::Base qw(parse_arguments load_pmodule2);
use Abills::Misc;
use SNMP_Session;
use SNMP_util;

our(
  %conf
);

require 'libexec/config.pl';

my $debug = 0;
my $argv = parse_arguments(\@ARGV);
my $Telnet;
if($argv->{DEBUG}) {
  $debug = $argv->{DEBUG};
}

if($debug) {
  print "HUAWEI Registration\n";
}

if($debug > 5) {
  my $ARGVS;  
  foreach my $line (@ARGV) {
    $ARGVS .= "$line ";
  }
  print @ARGV;
  `echo "@ARGV" >> /tmp/register`;
}


my %service_ports = ();
_get_service_port();
if ( !%service_ports ) {
  print "system busy please try again later";
  exit;
}
#use Data::Dumper;
#print Dumper $argv;
if ($argv->{onu_registration}) {
  my $register_fn = 'register_' . $argv->{TYPE} . '_huawei';
  if (defined( &$register_fn )) {
    &{ \&$register_fn }();
  }
  else {
    print 'Not exist "' . $register_fn . '()"';
  }
}
#**********************************************************
=head2 register_huawei($attr) - List of cards


=cut
#**********************************************************
sub register_gpon_huawei {
  my ($frame, $slot, $port) = split(/\//, $argv->{BRANCH}, 3);
  my $line_profile       = $argv->{LINE_PROFILE};
  my $srv_profile        = $argv->{SRV_PROFILE};
  my $vlan               = $argv->{VLAN_ID} || '';
  my $onu_type           = $argv->{TYPE};
  my $mac_serial         = $argv->{MAC_SERIAL};
  my $line_profile_vlans = $argv->{LINE_PROFILE_DATA};
  my $onu_desc           = ($argv->{ONU_DESC}) ? "desc $argv->{ONU_DESC}" : "; enter;";
  my $onu_id = '';
  my $err = 0;  
  my $vlan_count = 0;
  my $line_profiles_data = ();

  if (!$line_profile) {
    print 'Not exist "Line-Profile"' . "\n";
    $err = 1;
  }    
  if (!$srv_profile) {
    print 'Not exist "Srv-Profile"' . "\n";
    $err = 1;
  }
  if (!$vlan && defined($argv->{VLAN_ID})) {
    print 'Vlan is not selected' . "\n";
    $err = 1;
  }
  if (!$mac_serial) {
    print 'Not exist "Mac or Serial"' . "\n";
    $err = 1;
  }
  if (!$line_profile_vlans) {
    print 'Not exist Vlan in Line-Profile ' . $line_profile . "\n";
    $err = 1;
  }
  return 0 if ($err); 
  my @Line_profile_arr = split(';', $line_profile_vlans);
  foreach my $line (@Line_profile_arr) {
     my ($gem_port, $vlans) = split(':', $line);
     my @vlans_arr = split(',', $vlans);
     foreach my $vlan_id (@vlans_arr) {
       push @{ $line_profiles_data->{ $gem_port } }, $vlan_id;
       $vlan_count++;
     }
  }
  my @onu_service_ports = ();
  if (telnet_open()) {
    my $data = ''; 
    $data = telnet_cmd("interface gpon $frame/$slot");
    $data = telnet_cmd("ont confirm $port sn-auth $argv->{MAC_SERIAL} omci ont-lineprofile-name $line_profile ont-srvprofile-name $srv_profile $onu_desc");
    if ( $data =~ /.*ONTID \:([0-9]+).*/) {
      $onu_id = $1;
      print "ONU added $frame/$slot/$port:$onu_id\n";
    }
    else {
      print "FATAL ERROR: Unable to register ONT \n" if ($debug < 9);
      $data =telnet_cmd("quit") if ($debug < 9);
      $Telnet->close();
      return 0 if ($debug < 9);
    }
    if ($vlan && $vlan_count eq 1) {
      foreach my $gem_port (keys %$line_profiles_data) {
        $data = telnet_cmd("ont port native-vlan $port $onu_id eth $gem_port vlan $line_profiles_data->{$gem_port}->[0] priority 0");
      }
    }
    $data = telnet_cmd("quit");
    foreach my $gem_port (keys %$line_profiles_data) {
      foreach my $vlan_id (@{ $line_profiles_data->{$gem_port} }) { 
        my $vlan_s_p = $vlan || $vlan_id;
        my $service_port_num = _get_service_port();
        push @onu_service_ports, $service_port_num;  
        $data = telnet_cmd("service-port $service_port_num vlan $vlan_s_p gpon $frame/$slot/$port ont $onu_id gemport $gem_port multi-service user-vlan $vlan_id tag-transform translate; enter;");
        if ( $data =~ /.* existed .*/ ) {
          print "Failure: Service virtual port $service_port_num has existed already\n";
        }
        else {
          print "Service-port $service_port_num addded vlan $vlan_s_p in user_vlan $vlan_id\n";
          if (!$vlan && $vlan_count > 1) {
            $data = telnet_cmd("dhcp option82 service-port $service_port_num disable");
            print "Ðžption 82 in the service port $service_port_num is disabled\n";
          }
        }
      }
    }
    $Telnet->close();
    return 1;
  }
}
#**********************************************************
=head2 telnet_open($attr);

  Arguments:
    $attr
      NAS_INFO

  Returns:
    TRUE or FALSE

=cut
#**********************************************************
sub telnet_open{

  my $load_data = load_pmodule2('Net::Telnet', {SHOW_RETURN => 1});
  if ($load_data) {
    print $load_data . "\n";
    return 0;
  }

  if(! $argv->{NAS_MNG_IP_PORT}) {
    print "NAS_MNG_IP_PORT not defined \n";
    return 0;
  }

  my $user_name = $argv->{NAS_MNG_USER} || q{};
  my $password  = $argv->{NAS_MNG_PASSWORD} || q{};

  $Telnet = Net::Telnet->new(
    Timeout  => 15,
    Errmode  => 'return',
    Input_log => "/tmp/in.log",
    Output_log => "/tmp/out.log"
  );

  my ($ip, $mng_port, undef) = split(/:/, $argv->{NAS_MNG_IP_PORT}, 3);
  my $port = $mng_port || 23;
  $Telnet->open(
    Host  => $ip,
    Port  => $port
  );

  if ($Telnet->errmsg) {
    print "Problem connecting to $ip, port: $port\n";
    return 0;
  }
  $Telnet->waitfor('/>>User name:/i');
  if ($Telnet->errmsg) {
    print ">Problem connecting to $ip, port: $port\n";
    return 0;
  }

  $Telnet->print($user_name);
  $Telnet->waitfor('/>>User password:/i');
  $Telnet->print($password);
  if ($Telnet->errmsg) {
    print "Telnet login or password incorrect\n";
    return 0;
  }
  $Telnet->print(" ") or print "ERROR USER OR PASS";;
  $Telnet->print(" ") or print "ERROR USER OR PASS";
  $Telnet->waitfor('/>/i') || print "ERROR USER OR PASS";
  $Telnet->print("enable");
  $Telnet->waitfor('/#/i');
  $Telnet->print("config");
  $Telnet->waitfor('/\(config\)#/i');
  if ($Telnet->errmsg) {
    print "Telnet login or password incorrect";
    return 0;
  }
  return 1;
}

#**********************************************************
=head2 telnet_cmd($cmd);

=cut
#**********************************************************
sub telnet_cmd{
  my ($cmd) = @_;
  my @cmd_arr = split(';', $cmd);
  foreach my $cmd (@cmd_arr) {
    if ($cmd =~/enter/) {
      $Telnet->print("") if ($debug < 9);
    }
    else {
      print $cmd . " \n" if ($debug > 3);
      $Telnet->print("$cmd ") if ($debug < 9);
    }
  }
  my @data = $Telnet->waitfor('/#/i');
  $data[0] =~ s/\n/ /g;
  if ($data[0] =~ /.*System is busy.*/) {
    print "System is busy, please wait.";
    $Telnet->print("quit");
    $Telnet->waitfor('/#/');
    $Telnet->close();
    exit 0;
  }
  return $data[0];
}
#**********************************************************
=head2 _get_service_port($cmd);

=cut
#**********************************************************
sub _get_service_port {
  my $free_service_port;
  if ($argv->{TYPE} eq 'gpon') {
    my $SNMP_COMMUNITY = ($argv->{NAS_MNG_PASSWORD} || '') . '@' . (($argv->{NAS_MNG_IP_PORT}) ? $argv->{NAS_MNG_IP_PORT} : $argv->{NAS_IP});
    my $oid = '.1.3.6.1.4.1.2011.5.14.5.2.1.2';
    if ( !%service_ports ) {
      my $data = snmp_get({
        %{$argv},
        WALK    => 1,
        OID     => $oid,
        VERSION        => $argv->{SNMP_VERSION},
        SNMP_COMMUNITY => $SNMP_COMMUNITY,
        #TIMEOUT => 8,
        SILENT  => 1
      });
      foreach my $line (@$data) {
        my ($service_port, undef) = split(':', $line);
        $service_ports{ $service_port } = 1;
      }
      return;
    }
    for (my $num = 2 ; $num <= 20000 ; $num++) {
      if (!$service_ports{ $num }) {
         $free_service_port = $num;
         $service_ports{ $num } = 1;
         last;
      }
    }
  }
  return $free_service_port;
}
